ACTOR SawDirection: Inventory {Inventory.MaxAmount 1}

Actor BDPChainsaw : BrutalWeapon
{
	Weapon.SlotNumber 1
	Weapon.SlotPriority 2
	weapon.selectionorder 2200
	weapon.readysound "weapons/chainsaw/idle"
	+WEAPON.AXEBLOOD
	+WEAPON.MELEEWEAPON
	Inventory.PickupMessage "You got the Chainsaw! Find some meat! (Slot 1)"
	Weapon.KickBack 0
	Tag "$BDPSAW"
	Inventory.PickupSound "weapons/chainsaw/draw"
	Inventory.AltHUDIcon "CSAWA0"
	States
	{
	Spawn:
		CSAW A -1
		Stop
	//This state is set when spawned after being thrown
	Spawnthrown:
		CSAW B 8 {A_StartSound("weapons/chainsaw/idle", 6); A_SpawnProjectile("SmokeSpawner");}
		Loop
	Grenade: //32
		SAWS RSTU 1
		TNT1 A 24
		SAWS UTSR 1
		Goto Ready
	Kick: //16
		SAWS VWXY 1
		SAWS "[Z[Z" 2
		SAWS YXWV 1
		Goto Ready
	AirKick: //18
		SAWS VWXY 1
		SAWS "[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z[" 2
		SAWS YXWV 1
		Goto Ready
	SlideKick: //26
		SAWS VWXY 1
		SAWS "[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z[" 2
		SAWS YXWV 1
		Goto Ready
	Taunt: //30
		SAWS VWXY 1
		SAWS "[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z[Z" 2
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z" 1
		SAWS YXWV 1
		Goto Ready
	Sprint:
		SAWS VWXY 1 A_WeaponReadyDX(0,false,false)
	SprintLoop:
		SAWS "[Z" 2 A_KeepSprinting
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS "[Z" 2 A_KeepSprinting
		Loop
	SprintEnd:
		SAWS YXWV 1 A_WeaponReadyDX(0,false,false)
		Goto Ready
	Select:
		TNT1 A 1 {A_SetCrosshairDX("Null"); A_GiveInventory("HasCutingWeapon",1);}
		SAWS NOPQ 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_StartSound("DSSAWZIP", 1)
		SAWS A 3 A_WeaponReady(WRF_NOFIRE)
		SAWS BCCDDEFG 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_StartSound("weapons/chainsaw/draw", 3)
		SAWS HIJKLM 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	Deselect:
		TNT1 A 0 A_TakeInventory("HasCutingWeapon",1)
		SAWS RSTU 1
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_Stopsound(1)
		TNT1 A 1
		TNT1 A 0 A_Lower
		Wait
	Reload:
		TNT1 A 0 A_StartSound("DSSAWZIP", 1)
		SAWS A 3 A_WeaponReadyDX(WRF_NOFIRE)
		SAWS BCCD 1 A_WeaponReadyDX(WRF_NOFIRE)
	FidgetLoop:
		SAWS D 1 A_WeaponReadyDX(WRF_NOFIRE)
		TNT1 A 0 A_PressingReload("FidgetLoop")
		SAWS EFG 1 A_WeaponReadyDX(WRF_NOFIRE)
		TNT1 A 0 A_StartSound("weapons/chainsaw/draw", 3)
		SAWS HIJKLM 1 A_WeaponReadyDX(WRF_NOFIRE)
		Goto Ready
	Ready:
		TNT1 A 0 {A_StopSound(1); A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		SAWG ABCDEFGG 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,true,true,true)
		TNT1 A 0 A_JumpIf(A_GetBlood == 0,"Ready")
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		1AWB ABCDEFGG 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,true,true,true)
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		1AWB HIJKLMNN 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,true,true,true)
		Loop
	Fire:
		TNT1 A 0 A_StopSound(6)
		TNT1 A 0 A_JumpIfInventory("SawDirection",1,"Fire2")
		SAWG HIJ 1
		TNT1 A 2
	FireNoStartup:
		TNT1 A 0 A_BDPMeleeStart(300)
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1)
		SAWF AB 1 A_BDPMeleeStart(300)
		TNT1 A 0
		{
			A_StartSound("weapons/chainsaw/draw", 5);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_QuadSound;
		}
		SAWF CDE 1 A_BDPMelee(200,"MeleeAttackSaw",0,false)
		SAWF F 1 A_BDPMelee(200,"MeleeAttackSaw",0,TRUE)
		SAWF GHI 1
		TNT1 A 0 {A_SemiFlag; A_GiveInventory("SawDirection",1);}
		SAWF [ 8 A_WeaponReadyDX(WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_SemiRefire("Fire2NoStartup","")
		SAWG MLK 1
		Goto Ready
	Fire2:
		SAWG KLM 1
		TNT1 A 2
	Fire2NoStartup:
		TNT1 A 0 A_BDPMeleeStart(300)
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1)
		SAWF JK 1 A_BDPMeleeStart(300)
		TNT1 A 0
		{
			A_StartSound("weapons/chainsaw/draw", 5);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_QuadSound;
		}
		SAWF LMN 1 A_BDPMelee(200,"MeleeAttackSaw",0,false)
		SAWF O 1 A_BDPMelee(200,"MeleeAttackSaw",0,TRUE)
		SAWF PQR 1
		TNT1 A 0 {A_SemiFlag; A_TakeInventory("SawDirection",1);}
		SAWF [ 8 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB)
		TNT1 A 0 A_SemiRefire("FireNoStartup","")
		SAWG JIH 1
		Goto Ready
	AltFire:
		SAWG NOPPQ 1
	FireLoop:
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1)
		TNT1 A 0 A_QuadSound
		SAWG RSTU 1 A_Saw("", "", 1, "SSawPuff2", SF_NOPULLIN | SF_NOTURN, 100, 32)
		TNT1 A 0 A_ReFire("FireLoop")
		SAWG VWON 1
		Goto Ready
	SpecialAction:
		SAWG HIJ 1
		TNT1 A 2
		SAWF CD 1
		TNT1 A 0 A_StartSound("weapons/gswing", 1)
		TNT1 A 0 A_FireProjectile("ThrowedChainsaw", 0, 0, 0, 0)
		TNT1 A 0 A_QuadSound
		AXEZ ABC 1
		{
			A_SetAngle(Angle+2, SPF_INTERPOLATE);
			A_SetPitch(Pitch+1, SPF_INTERPOLATE);
		}
		AXEZ D 1
		{
			A_SetAngle(Angle+1, SPF_INTERPOLATE);
			A_SetPitch(Pitch+0.5, SPF_INTERPOLATE);
		}
		AXEZ E 1
		{
			A_SetAngle(Angle+0.5, SPF_INTERPOLATE); //7,5
			A_SetPitch(Pitch+0.2, SPF_INTERPOLATE); //3,7
		}
		AXEZ E 2
		AXEZ E 1
		{
			A_SetAngle(Angle-0.5, SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.2, SPF_INTERPOLATE);
		}
		AXEZ EEF 1
		{
			A_SetAngle(Angle-2, SPF_INTERPOLATE);
			A_SetPitch(Pitch-1, SPF_INTERPOLATE);
		}
		AXEZ G 1
		{
			A_SetAngle(Angle-1, SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.5, SPF_INTERPOLATE);
		}
		AXEZ HI 1
		TNT1 A 1 A_jumpifinventory("PowerSpeed2",1,"Select")
		TNT1 A 0 A_GiveInventory("iJustThrowedMySaw",1)
		TNT1 A 0 A_TakeInventory("HasCutingWeapon",1)
		TNT1 A 0 A_SelectWeapon("BDPFist")
		Goto InstaDeselect
	}
}

ACTOR SawDamageVertical : FastProjectile
{
	Radius 22
	Height 2
	DamageType Saw
	Projectile 
	+FORCEXYBILLBOARD
	+EXTREMEDEATH
	+BLOODSPLATTER
	+MTHRUSPECIES
	RenderStyle Add
	Alpha 0.6
	Damage (8)
	Speed 28
	SeeSound "none"
	DeathSound "none"
	Decal "SawVertical"
	Species "Marines"
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	Death:
		TNT1 A 11 Radius_Quake(2, 3, 0, 4, 0)
		Stop
	XDeath:
		TNT1 A 1 Radius_Quake(2, 3, 0, 4, 0)
		TNT1 A 10 A_StartSound("CSAWFLESH", 1)
		Stop
	}
}

ACTOR iJustThrowedMySaw: Inventory {Inventory.maxAmount 1}