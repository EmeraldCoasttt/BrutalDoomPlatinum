ACTOR DoxsRevolver : BrutalWeapon
{
	Weapon.AmmoGive1 15
	Weapon.AmmoType "Clip1"
	BrutalWeapon.MagCapacity 6
	Weapon.SlotNumber 2
	Weapon.SlotPriority 3
	weapon.selectionorder 3800
	Obituary "%o was shot down by %k's revolver."
	Inventory.PickupSound "REVDRY"
	Inventory.Pickupmessage "You got the Revolver! Yee-haw! (slot 2)"
	Scale 0.9
	Tag "$BDPREVOLVER"
	Inventory.AltHUDIcon "REVOQ0"
	Inventory.MaxAmount 2
	BrutalWeapon.Handedness 1
	BrutalWeapon.DualWeapon "DualRevolver"
	States
	{
	Grenadethrowflash:
		REVS ABC 1
		TNT1 A 26
		REVS CBA 1
		Stop
	KickingFlash:
		REVR CIJKLLLLLLLLKJIC 1
		Goto Ready
	AirKickingFlash:
		REVR CIJKLLLLLLLLLLKJIC 1 //18
		Goto Ready
	SlideKickingStart:
		REU1 CIJKL 1
	SlideKickingLoop:
		REVR L 1 A_CheckSlide
		Loop
	 SlideKickingEnd:
		REVR LLLLLLKJIC 1 //10
		Goto Ready
	FuckYouFlash:
		REVR CIJK 1 //30
		REVR L 22
		REVR KJIC 1
		Stop
	SprintOverlay:
		REVR CIJK 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE)
	KeepSprinting:
		REVR L 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE)
		TNT1 A 0 A_KeepSprinting
	ReturnFromSprint:
		REVR KJIC 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE)
		Goto Ready
	Ready:
		REVO A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD)
		TNT1 A 0 A_AutoReloadMag
		Loop
	Deselect:
		REVS ABC 1
		TNT1 A 1
		TNT1 A 0 A_Lower
		Wait
	Select:
		TNT1 A 1 {A_SetCrosshairDX("SMGRet",2000,0.3);A_GunSlingerMag;}
		REVO A 0 A_StartSound("REVCOCK")
		REVS CBA 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	SelectFromDual:
		REVS ONML 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
	Fire:
		REVO A 0 A_CheckIfMag
		REVO A 1 A_StartSound("REVFIRE1")
		REVO M 1 Bright
		{
			A_FireBullets (2, 2, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(3,2,-1);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX;
			A_ZoomFactor(0.98);
			A_QuadSound;
			A_GunLight;
			A_TakeMag;
		}
		REVO B 1 BRIGHT A_SetPitch(Pitch-4, SPF_INTERPOLATE)
		REVO A 0 A_ZoomFactor(1.0)
		REVO CDEE 1
		TNT1 A 0 A_semiflag
		REVO DC 1 A_SetPitch(Pitch+0.5, SPF_INTERPOLATE)
		REVO FGHIJ 1 A_SetPitch(Pitch+0.6, SPF_INTERPOLATE)
		REVO A 2
		TNT1 A 0 A_SemiRefire("Fire","Ready")
		Goto Ready
	AltFire:
		REVO NOP 1
		Goto Altfire2
	Altfire2:
		TNT1 A 0 A_CheckIfMag(1,"NoAmmo3")
		TNT1 A 0 A_StartSound("REVFIRE1")
		RE2F ABCC 1
		RE2F D 1 Bright
		{
			A_FireBullets (4, 3, 1, 40 , "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(3,2,-1,2);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX;
			A_ZoomFactor(0.98);
			A_QuadSound;
			A_GunLight;
			A_TakeMag;
		}
		RE2F E 1
		TNT1 A 0 A_SetPitch(Pitch-3, SPF_INTERPOLATE)
		TNT1 A 0 A_ZoomFactor(1.0)
		RE2F FGH 1 A_SetPitch(Pitch+1, SPF_INTERPOLATE)
		TNT1 A 0 A_Refire("Altfire2")
		REVO PON 1
		Goto Ready
	NoAmmo:
		REVO A 0 A_StartSound("REVFIRE1")
		REVO KLL 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_StartSound("REVDRY", 4)
		TNT1 A 0 offset(0,34)
		REVO A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 offset(0,33)
		REVO A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 offset(0,32)
		REVO A 1 A_WeaponReady(WRF_NOFIRE)
	NoAmmo2:
		TNT1 A 0 A_AutoReloadMag
		REVO A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK) || GetPressed(BT_ALTATTACK), "NoAmmo2")
		Goto Ready
	NoAmmo3:
		REVO A 0 A_StartSound("REVFIRE1")
		RE2F ABC 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 offset(0,34) A_StartSound("REVDRY", 4)
		RE2F A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 offset(0,33)
		RE2F A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 offset(0,32)
		RE2F A 1 A_WeaponReady(WRF_NOFIRE)
	NoAmmo4:
		TNT1 A 0 A_AutoReloadMag
		RE2F A 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK) || GetPressed(BT_ALTATTACK), "NoAmmo4")
		REVO PON 1
		Goto Ready
	Fidget:
		RSDF JL 1 A_WeaponReadyDX(0,false,false)
		RFDF KIG 1 A_WeaponReadyDX(0,false,false)
		TNT1 A 0 A_StartSound("REVFLIP",13)
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false)
		TNT1 A 0 A_StartSound("REVFLIP",13)
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false)
	FidgetLoop:
		TNT1 A 0 A_StartSound("REVFLIP",13)
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false)
		TNT1 A 0 A_StartSound("REVFLIP",13)
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false)
		TNT1 A 0 A_PressingReload("FidgetLoop")
		RFDF GIK 1 A_WeaponReadyDX(0,false,false)
		RSDF LJ 1 A_WeaponReadyDX(0,false,false)
		Goto Ready
	Reload:
		REVR ACFGHIJKLLH 1
		TNT1 A 0 A_StartSound("REVOPEN")
		REVR MNOPPQRSTTT 1
		TNT1 A 0 A_DiscardMagazine
		TNT1 AAAAAA 0 A_FireProjectile("RevolverCaseSpawn",random(70,90),0,1,-8)
		TNT1 A 0 A_StartSound("REVFLY")
		REVR WXYUUU 1
		REV2 ABCDDDEFGHHIII 1
		TNT1 A 0 A_StartSound("REVINSRT")
		TNT1 A 0 A_ReloadMag
		REV2 JKKKKJLMNOOO 1
		REV3 ABDF 1
		TNT1 A 0 A_StartSound("REVCLOSE")
		REV3 GHIIJKLMN 1
		Goto Ready
	Spawn:
		REVO Q -1
		LOOP
	DualWield:
		TNT1 A 0 A_DualWield
		Goto Ready
	}
}

ACTOR RevolverCaseSpawn : RifleCaseSpawn
{
	Speed 1
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"spawn1","spawn2","spawn3")
		Loop
	Spawn1:
		TNT1 A 0 A_CustomMissile("RevolverCasing",0,0,random(-80,-100),2,-1)
		Stop
	Spawn2:
		TNT1 A 0 A_CustomMissile("RevolverCasing2",0,0,random(-80,-100),2,-1)
		Stop
	Spawn3:
		TNT1 A 0 A_CustomMissile("RevolverCasing3",0,0,random(-80,-100),2,-1)
		Stop
	}
}

ACTOR RevolverCasing: PistolCasing
{
	Speed 1
	Scale 0.15
	BounceFactor 0.4
	SeeSound "weapons/casing"
	States
	{
	Spawn:
		C4S1 FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHH 1
		Stop
	Death:
		C4S1 I 0
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor")//Check Effects
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor2")//Check Effects
		LCPJ A 0 A_Jump(256,"Rest1","Rest2","Rest3","Rest4","Rest5")
		Goto Rest1
	Rest1:
		C4S1 I 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Rest2:
		C4S1 J 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Rest3:
		C4S1 K 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Rest4:
		C4S1 L 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Rest5:
		C4S1 M 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Splash:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("UnderwaterEmptyBrass")
		Stop
	Stoping:
		TNT1 A 0
		Stop
	}
}

ACTOR RevolverCasing2: RevolverCasing
{
	BounceFactor 0.5
	States
	{
	Spawn:
		C4S1 FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFF 1
		Stop
	}
}

ACTOR RevolverCasing3: RevolverCasing
{
	BounceFactor 0.6
	States
	{
	Spawn:
		C4S1 FFGGHHAABBCCDDEEFFGGHHAABBCCDDEEFFGGHHAABBCCDD 1
		Stop
	}
}

ACTOR RevolverPuff : ShotgunPuff
{
	Damagetype "Revolver"
}