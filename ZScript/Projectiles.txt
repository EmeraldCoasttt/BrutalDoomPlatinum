Class BrutalProjectile : Actor
{
	//Thanks RicardoLuis0 for the seeker code

	bool Alive;
    double RotSpeed;
    double MaxRotAngle;
    double OffsetX;
    double OffsetY;
    double OffsetZ;
    
    Property RotSpeed : RotSpeed;
    Property MaxRotAngle : MaxRotAngle;
    Property Offset : OffsetX, OffsetY, OffsetZ;
	Default
	{
		Projectile;
		BrutalProjectile.rotspeed 15;
		BrutalProjectile.MaxRotAngle 360;
		BrutalProjectile.offset 0,0,0;
	}
	
	Override int specialmissilehit(actor Victim)
	{
		If(target && victim is "BaseHeadShot" && victim.tracer && victim.tracer == target)
		{
			Return 1;
		
		}
		Else
		{
			Return -1;
		}
	
	}
	
	 override int DoSpecialDamage(Actor target, int damage, name damagetype)
    {
        alive=false;
        return super.DoSpecialDamage(target, damage, damagetype);
    }
	
	 override void BeginPlay()
    {
        super.BeginPlay();
        Alive = true;
    }
	
	  private void Steer(Vector3 tpos)
    {
        vector3 targetAnglePitch = Level.SphericalCoords(pos, tpos);
        
        double adiff = DeltaAngle(angle, -targetAnglePitch.x);
        double pdiff = DeltaAngle(pitch, targetAnglePitch.y);
        
        if( abs(adiff) <= MaxRotAngle
         && abs(pdiff) <= MaxRotAngle)
        {
            if(abs(adiff) > RotSpeed)
            {
                angle += (adiff > 0) ? RotSpeed : -RotSpeed;
            }
            else
            {
                angle += adiff;
            }
            if(abs(pdiff) > RotSpeed)
            {
                pitch += (pdiff > 0) ? RotSpeed : -RotSpeed;
            }
            else
            {
                pitch += pdiff;
            }
            A_ChangeVelocity(
                cos(angle) * cos(pitch) * speed,
                sin(angle) * cos(pitch) * speed,
                sin(pitch) * speed,
                CVF_REPLACE
            );
        }
    }
	
	Action Void A_GuidedRocket()
	{
		let it = ThinkerIterator.Create("Actor", STAT_DEFAULT);
						Actor mo;
					while (mo = Actor(it.Next()))
			{
				If(mo is "GuidedLaserNEO")
				{
				invoker.tracer = mo;
				//console.printf("Gottem");
				//Break;
				}
			}
			If(invoker.tracer)
			{
				invoker.Steer(invoker.tracer.pos + (invoker.offsetX, invoker.offsetY, invoker.offsetZ));
			}
			//invoker.a_tracer2();
	}

}

Class FastBrutalProjectile : Actor
{
	Default
	{
		Projectile;
	}
	
	Override int specialmissilehit(actor Victim)
	{
		If(target && victim is "BaseHeadShot")
		{
			Return 1;
		
		}
		Else
		{
			Return -1;
		}
	
	}

}

class BEAMZ_LaserBeam : Actor
{
	Color shade;

	double maxDist;
	int ontics;
	bool enabled;
	Actor source;
	vector3 curPos;
	vector3 offsets;
	vector2 angleOffsets;
	vector3 curOffs;
	transient FLineTraceData hitData;
	
	bool trackingPos;
	vector3 targetPos;
	
	bool aimWithWeapon;
	bool trackPSprite;
	uint trackPSLayer;
	
	bool followAngles, continuousHit;
	Property TrackAngles : followAngles;
	Property TrackWeapon : trackPSprite, trackPSLayer;
	Property AimFromWeapon : aimWithWeapon; 
	Property LaserColor : shade;
	Property ContinuousImpact : continuousHit;
	
	Default
	{
		Scale 1.0;
		+NOINTERACTION;
		+INTERPOLATEANGLES;
		RenderStyle "Add"; 
		
		BEAMZ_LaserBeam.LaserColor "Blue";
		BEAMZ_LaserBeam.TrackAngles true;				// Update with player's view.
		BEAMZ_LaserBeam.TrackWeapon true, PSP_WEAPON; // Offset by PSprite offsets.
		BEAMZ_LaserBeam.AimFromWeapon true;			// Fire from weapon "muzzle", only used if TrackWeapon is enabled.
		BEAMZ_LaserBeam.ContinuousImpact false; 	// If true, calls the OnImpact function every tick the laser is enabled, instead of just once after the laser is fired.
	}
	
	static BEAMZ_LaserBeam Create(Actor source, double fw, double lr, double ud, double angleOffs = 0, double pitchOffs = 0, double maxDist = 2048, class<BEAMZ_LaserBeam> type = "BEAMZ_LaserBeam")
	{
		let laser = BEAMZ_LaserBeam(Spawn(type, source.pos));
		if(laser) 
		{
			laser.source = source;
			laser.maxDist = maxDist;
			laser.offsets = (fw, lr, ud);
			laser.angleOffsets = (angleOffs, pitchOffs);
		}
		
		return laser;
	}
	
	void setEnabled(bool set)
	{
		enabled = set;
	}
	
	void startTracking(vector3 toPos)
	{
		trackingPos = true;
		targetPos = toPos;
	}
	
	void aimAtCrosshair()
	{
		double zoffs = source.height*0.5;
		if(source.player) zoffs = source.player.viewz - source.pos.z;
	
		FLineTraceData lt;
		source.LineTrace(source.angle, maxDist, source.pitch, offsetz:zoffs, offsetforward:source.radius, data:lt);
		if(lt.HitType != TRACE_HitNone) 
		{
			vector3 aimAngles = level.SphericalCoords(curPos, lt.HitLocation, (source.angle,source.pitch));
			angleOffsets.x = aimAngles.x;
			angleOffsets.y = aimAngles.y;
		}
	}
	
	void stopTracking()
	{
		if(trackingPos) ontics = 0;
		trackingPos = false;
	}
	
	bool isTracking()
	{
		return trackingPos;
	}
	
	virtual vector3 getSourcePos()
	{
		vector3 srcPos = (source.pos.xy, source.pos.z + (source.height * 0.5));
		if(source.player) srcPos.z = source.player.viewz;
		
		return srcPos;
	}
	
	virtual void BeamTick()
	{
		// Implement custom laser logic here.
	}
	
	virtual void OnImpact(vector3 hitPos, Actor hitActor)
	{
		// On impact with something
	}
	
	override void Tick()
	{
		if(isFrozen()) return;
		if(!enabled || !source) 
		{
			ontics = 0;
			bInvisible = true;
			return;
		}
		ontics++;
		bInvisible = ontics < 3;
		//if(shade) SetShade(shade);
		
		if( ontics == 2 || (ontics >= 2 && continuousHit) )
		{ 
			OnImpact(hitData.hitLocation, hitdata.hitActor);
		}
				
		// PSprite tracking?
		vector2 bob = (0,0);
		if(trackPSprite && PlayerPawn(source))
		{
			let psp = source.player.GetPSprite(trackPSLayer);
			bob = PlayerPawn(source).BobWeapon(1.0);
			
			bob.x += psp.x;
			bob.y += (psp.y - 32);
			bob.x *= 0.031;
			bob.y *= 0.035;
		}
		
		// Update laser and tracking.
		let aimDir = Quat.FromAngles(source.angle, source.pitch, source.roll);
		curOffs = aimDir * (
			offsets.x, 
		  -(offsets.y + bob.x), 
			offsets.z - bob.y
		);
		
		vector3 finalPos = level.vec3offset(getSourcePos(), curOffs);
		SetOrigin(finalPos, true);
		curPos = finalPos;
		
		double toAngle = source.angle + angleOffsets.x;
		double toPitch = source.pitch + angleOffsets.y;
		if(aimWithWeapon) 
		{
			toAngle -= (bob.x * 10);
			toPitch += (bob.y * 10);  
		}	
		
		// Track target and source's angles.
		if(trackingPos)
		{
			vector3 diff = level.vec3diff(finalPos, targetPos);
			vector3 dir = diff.Unit();
			
			toAngle = angleOffsets.x + atan2(dir.y, dir.x) + 180; 
			toPitch = angleOffsets.y + asin(dir.z);
			A_SetAngle(toAngle, SPF_INTERPOLATE);
			A_SetPitch(toPitch - 90, SPF_INTERPOLATE);		
		} 
		else if(followAngles)
		{
			A_SetAngle(toAngle, SPF_INTERPOLATE);
			A_SetPitch(toPitch+90, SPF_INTERPOLATE);
		}
		
		// View Interpolation.	
		if(source.player) 
		{
			if(source.player.cheats & CF_PREDICTING) return;
			source.player.cheats |= CF_INTERPVIEW;
		}
		
		// Do linetrace to determine aim distance.
		double zoffs = source.player ? (source.player.viewz-source.pos.z) : source.height * 0.5;
		source.LineTrace(angle, maxDist, pitch - 90, 0, zoffs+offsets.z-bob.y, offsets.x, offsets.y-bob.x, data:hitData);
		
		// Scale to Distance.
		double dist = min(hitData.Distance, maxDist);
		double dirPitch = pitch - 90;
		scale.y = dist * level.pixelstretch;
					
		BeamTick();
	}
	
	States
	{
		Spawn:
			MODL A -1 Bright;
		stop;
	}
}

class TenderizerLaser : BEAMZ_LaserBeam
 {
	Default
	{
		//BEAMZ_LaserBeam.LaserColor "Red";
	}
	
	override void BeamTick()
	{
		//alpha = sin(GetAge() * 60) + 0.5;
		aimAtCrosshair();
	}
 }