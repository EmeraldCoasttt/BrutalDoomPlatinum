CLASS BDPIncinerator : BDPWeaponBase
{
	Default
	{
		Height 20;
		Weapon.SlotNumber 5;
		Weapon.SlotPriority 3;
		Weapon.SelectionOrder 3100;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "BDPRocketAmmo";
		BDPWeaponBase.MagCapacity 60;
		BDPWeaponBase.MagConversion (10,1);
		Inventory.PickupSound "FLMRPUP";
		+WEAPON.EXPLOSIVE
		Inventory.PickupMessage "You got the Incinerator! (Slot 5)";
		Tag "$BDPFLAMER";
		Inventory.AltHUDIcon "FLMTF0";
	}
	States
	{
	Grenade: //30
		FLRS BCDE 1;
		TNT1 A 22;
		FLRS EDCB 1;
		Goto Ready;
	Kick: //18
		FLMK ABCD 1;
		FLMK E 10;
		FLMK DCBA 1;
		Goto Ready;
	SlideKick: //25
		FLMK ABCD 1;
		FLMK E 17;
		FLMK DCBA 1;
		Goto Ready;
	Taunt: //30
		FLMK ABCD 1;
		FLMK E 22;
		FLMK DCBA 1;
		Goto Ready;
	Sprint:
		FLMK ABCD 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		FLMK E 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		FLMK DCBA 1 A_SetSprites("TNT1A0",1);
		Stop;
	Ready:
		FLMT ABC 1 A_weaponreadyDX(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		TNT1 A 0 A_StopSound(CHAN_WEAPON);
		FLRS ABCDE 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("FLAMRet", 800); A_GunSlingerMag();}
		TNT1 A 0 A_StartSound("FLMRDRAW");
		FLRS EDCBA 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Spawn:
		FLMT F 1;
		Loop;
	Fire:
		TNT1 A 0 A_JumpIf(waterlevel > 1, "NoAmmo");
		TNT1 A 0 A_CheckIfMag();
		FLMT D 1 Bright
		{
			A_Fireprojectile("NewIncineratorFlame",0,false,0,-12);
			A_FireParticles(3);
			A_StartSound("FLAMER", 1, CHANF_LOOPING);
			A_QuadSound();
			A_GunLight(750,2);
			A_TakeMag();
		}
		FLMT E 1 Bright A_GunLight(600,2);
		TNT1 A 0 A_JumpIf(invoker.mag > 0, 2);
		TNT1 A 0 A_StartSound("FLAMSTOP", 1);
		Goto NoAmmo;
		TNT1 A 0 A_RefireDX();
		TNT1 A 0 A_GunLight(500,3);
		TNT1 A 0 A_StartSound("FLAMSTOP", 1);
		Goto Ready;
	Altfire:
		FLMG A 0 A_JumpIf(waterlevel > 1, "NoAmmo");
		FLMG A 0 A_CheckIfMag(5);
	SmokeFireball:	
		NULL A 0 A_StartSound("weapons/firegrenade");
		TNT1 A 0 a_alertmonstersDX();
		FLMT D 1 Bright
		{
			A_FireCustomMissile("SmokeSpewer", 0, 0, 0, -6);
			A_FireParticles(12);
			A_StartSound("weapons/firegrenade");
			A_alertmonstersDX();
			A_TakeMag(5);
		}
		FLMT D 1 Bright
		{
			A_WeaponOffset(random(-1,1),32+random(0,1) );
			A_SetPitch(-4.0 + pitch, SPF_INTERPOLATE);
			A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0);
		}
		FLMT E 1 Bright;
		FLMT AB 1 A_SetPitch(+2.0 + pitch, SPF_INTERPOLATE);
		FLMT CABC 1;
		Goto Ready;
	DoReload:	
		FLMT K 1 A_Startsound("FLMRUNLD");
		FLMT OPQRSS 1;
		FLMT T 1 {A_DiscardMag(); A_FireProjectileDX("emptyflamercan",3,-30,30,0,-10);}
		FLMT ZZTSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 1;
		FLMT T 1 {A_ReloadMag(); A_Startsound("FLMRLOAD");}
		FLMT TZZTSSSSSSSSSTUVWXYK 1;
		Goto Ready;
		
	}
}





class NewIncineratorFlame : Actor // Incinerator Flame 
{
	Default
	{
		Damagefunction 10;
		Speed 40;
		Radius 8;
		Height 8;
		
		Projectile;
		+FORCERADIUSDMG;
		RenderStyle "Add";
		Damagetype "fire";
		//+rollsprite;
		Decal "FlamethrowerScorchNEO";
		+bounceonfloors;
		+bounceonceilings;
		bouncecount 2;
		+FORCEXYBILLBOARD
	}

	States
	{
	Spawn:
		TNT1 A 0 BRIGHT;
		TNT1 A 0
		{
			Double scalefactor = frandom(0.2,0.3);
			A_setscale(scalefactor,scalefactor);
		}
		FIR3 AB 1 BRIGHT;
		TNT1 A 0
		{
			Double scalefactor = frandom(0.78,1.0);
			A_setscale(scalefactor,scalefactor);
		}
		FIR3 ABABABABCDCDCDEFGHIJKLMNOP 1 BRIGHT;
		Stop;
		
	Death:
		TNT1 A 0
		{
			Double scalefactor = frandom(0.78,1.0);
			A_setscale(scalefactor,scalefactor);
		}
		TNT1 A 0 A_Explode(10, 128, 1, 0, 1);
		FIR3 BCDEFGHIJKLMNOP 1 BRIGHT;
		Stop;
	}
	
	Override int SpecialMissileHit(Actor victim) 
		{
			If(victim && target && victim != target && !(victim is "baseheadshot") && victim.bshootable)
			{
				If(!Victim.findinventory("firetic"))
				{
					Victim.giveinventory("firetic",1);
				}
				Actor Flameactor = Victim.Findinventory("Firetic");
				If(Flameactor)
				{
					let flamer = Firetic(flameactor);
					if(flamer)
					{
						flamer.flamecauser = target;
						if(flamer.duration < 90)
						flamer.duration = 90;
						If(flamer.duration < 500)
						flamer.duration += 20;
					}
				
				}
				
			}
		
			Return -1;
		}
}

Class Firetic : Inventory {
	
	
	float duration; 
	actor flamecauser;
	Default {
		+INVENTORY.UNDROPPABLE
		+INVENTORY.UNTOSSABLE
		inventory.maxamount 1;
	}
	

	

	//Items that don't spawn in the world don't need their Tick. Emptying it will actually help performace:
	Override void Tick() {}
	
	//This function is executed every tic by items while they're in an actor's inventory:
	Override void DoEffect() {
		super.DoEffect();
		//console.printf("%i",duration);
		if (owner.isFrozen())
			return;
		if (level.time % 10 == 0) 
		{
			owner.damagemobj(flamecauser,flamecauser,7,"Fire");
			If(owner)
			owner.A_Explode(3, 200, XF_HURTSOURCE, 0, 1,DAMAGETYPE:"FIRE");
		}
		if (owner && owner.health <= 0) {		
			DepleteOrDestroy();
			return;
		}
		else if(owner)
		{
		
		
			int rad = owner.radius*0.5; //we'll use the monster's radius as a base value for horizontal offset randomization
			If (random(1,5) == 5)
			{
			
			
			owner.A_SpawnItemEx(
				"flametrailstic2",
							xofs:(frandom(-rad,rad)),
				yofs:(frandom(-rad,rad)),
				zofs:(frandom(0,owner.height)),xvel:frandom(-2,2),yvel:frandom(-2,2),zvel:frandom(0.5,3)
				);
			}
			Else
			{
				owner.A_SpawnItemEx(
				"flametrailstic",
							xofs:(frandom(-rad,rad)),
				yofs:(frandom(-rad,rad)),
				zofs:(frandom(0,owner.height)),xvel:frandom(-2,2),yvel:frandom(-2,2),zvel:frandom(0.5,3)
				);
			}
		}
		
			if (duration > 0)
				duration--;
			else {
				DepleteOrDestroy();
				return;
			}
		

		


		
	}
}
