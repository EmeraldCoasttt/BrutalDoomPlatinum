CLASS BDPBattleRifle : BDPWeaponBase
{
	Default
	
	{
	Weapon.AmmoGive 30;
	Weapon.AmmoType "BDPClip2";
	BDPWeaponBase.magcapacity 15;
	Obituary "%o was pierced by %k's Battle Rifle.";
	Inventory.PickupSound "BR45PICK";
	Inventory.Pickupmessage "You got the Battle Rifle! (Slot 4)";
	Weapon.SlotNumber 4;
	Weapon.SlotPriority 2;
	Weapon.SelectionOrder 1550;
	Inventory.AltHUDIcon "BR45A0";
	Tag "$BDPBR";
	

	}
	bool burstselected;
	int burstcount;
	int savedzoom;
	
	Action void a_FireBattleRifle()
	{
	
		FLineTraceData ricochetdata;
		invoker.owner.LineTrace(invoker.owner.angle, 4096, invoker.owner.pitch, TRF_SOLIDACTORS, offsetz: invoker.owner.player.viewz - invoker.owner.pos.z, data: ricochetdata);
		vector3 hitNormal;
		if(ricochetdata.HitType == TRACE_HitWall )
		{
			if(!ricochetdata.LineSide)
			{
				hitnormal = (ricochetdata.Hitline.delta.y, -ricochetdata.Hitline.delta.x, 0).unit();
			}
			else
			{
				hitnormal = (-ricochetdata.Hitline.delta.y, ricochetdata.Hitline.delta.x, 0).unit();
			}
			
		}
		else if (ricochetdata.HitType == TRACE_HitFloor)
		{
			hitnormal = ricochetdata.HitSector.FloorPlane.normal;
			
		}
		else if (ricochetdata.HitType == TRACE_HitCeiling)
		{
			hitnormal = ricochetdata.HitSector.CeilingPlane.normal;
		}
		Vector3 PlayerAngle = BDPMATH.AngletoVector3(1.0,invoker.owner.angle,invoker.owner.pitch);
		
		Vector3 BounceAngle = BDPMATH.BounceNormal(PlayerAngle,hitNormal);
		
		Double NextShotAngle;
		Double NextShotPitch;
		
		
		
		[NextShotAngle, NextShotPitch] = BDPMATH.Vector3toangles(BounceAngle);

		double anglediff = BDPMath.AngleDiff(invoker.owner.angle % 360.0,nextshotangle % 360.0);
		double pitchdiff = BDPMath.AngleDiff(invoker.owner.pitch % 360.0,nextshotpitch % 360.0);
		//Console.printf("%f",anglediff);
		
		
		Vector3 NextShotPosition = level.Vec3Offset(ricochetData.hitlocation, hitnormal * 2.0);
		
		A_FireBullets (0, 0, -1, 25, "BR45BulletPuff", FBF_NORANDOM,8192,"decorativetracer",-12);
		If(pitchdiff > 45 || anglediff > 45 || pitchdiff < -45 || anglediff < -45)
		{
			return;
		}
		If(ricochetdata.HitType == TRACE_HitWall || ricochetdata.HitType == TRACE_HitFloor || ricochetdata.HitType == TRACE_HitCeiling)
		{
			Invoker.Owner.LineAttack(NextShotAngle,8192,NextShotPitch,35,"Pistol","BR45BulletPuff",LAF_ABSPOSITION | LAF_ABSOFFSET,null,NextShotPosition.Z,NextShotPosition.x,NextShotPosition.y);
			let ricochettracer = Invoker.owner.spawn("decorativetracer",nextshotposition);
			If(ricochettracer)
			{
				ricochettracer.angle = nextshotangle;
				ricochettracer.pitch = nextshotpitch;
				ricochettracer.vel3dfromangle(140,nextshotangle,nextshotpitch);
			}
			
		}
	}
	
	States
	{
	Grenade: //30
		TNT1 A 0 A_EndAim();
		BR4S BCDE 1;
		TNT1 A 22;
		BR4S EDCB 1;
		Goto Ready;
	Kick: //18
		BR4K ABCD 1;
		BR4K E 10;
		BR4K DCBA 1;
		Goto Ready;
	SlideKick: //25
		BR4K ABCD 1;
		BR4K E 17;
		BR4K DCBA 1;
		Goto Ready;
	Taunt: //30
		BR4K ABCD 1;
		BR4K E 22;
		BR4K DCBA 1;
		Goto Ready;
	Sprint:
		BR4K ABCD 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		BR4K E 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		BR4K DCBA 1 A_SetSprites("TNT1A0",1);
		Stop;
	
		Select:
			TNT1 A 1 {A_SetCrosshairDX("BR45Ret",2000); A_GunSlingerMag();}
			TNT1 A 0 A_startSound("BR45PICK",1);
			BR4S EDCBA 1 A_WeaponReady(WRF_NOFIRE);
			GOto Ready;
		Deselect: 
			TNT1 A 0 A_EndAim();
			BR4S ABCDE 1;
			TNT1 A 0 A_StopSound(1);
			TNT1 A 0 A_StopSound(2);
			TNT1 A 0 A_StopSound(6);
			TNT1 A 1;
			TNT1 A 0 A_Lower();
			Wait;
		Ready:
			TNT1 A 0 
			{
				invoker.burstcount = 0;
				A_SetCrosshairDX("BR45Ret",2000);
			}
			BR45 B 1 A_weaponreadyDX(WRF_ALLOWRELOAD);
			LOOP;

		Fire:
			TNT1 A 0 A_CheckIfMag();
			BR4F A 0 A_Jump(85,3);
			BR4F B 0 A_Jump(85,2);
			BR4F C 0;
			BR4F "#" 1
			{
				A_FireBattleRifle();
				A_WeaponRecoil(1);
				A_ZoomFactor(0.99);
				A_FireParticles(1,8);
				A_FireBattleRifle();
				A_startsound("BR45FIRE",1,0,0.85);
				A_QuadSound();
				A_ALertMonstersDX();
				A_TakeMag();
				A_GunLight();
				invoker.burstcount++;
			}
			BR45 D 1
			{
				A_zoomfactor(1.0);
				A_fireprojectile("riflecasespawnversion2",5,false,0,-14,0);
				If(invoker.mag < 1)
				{
					A_fireprojectile("RifleClipSpawn",5,false,0,-14,0);
				}
			}
			TNT1 A 0 A_jumpif(invoker.burstcount < 3,"burstfirerecoil");
			TNT1 A 0 A_semiFlag();
			BR45 DEFGH 1;
			TNT1 A 0 {invoker.burstcount = 0;}
			TNT1 A 0 A_SemiRefire();
			Goto Ready;
			
			BurstFireRecoil:
			BR45 EF 1;
			Goto Fire;
		DoReload:
			TNT1 A 0 A_EndAim();
			TNT1 A 0 A_startsound("BR45OPEN",3,CHANF_OVERLAP);
			BR4R ABCDE 1;
			TNT1 A 0
			{
			If(invoker.mag > 0)
				{
					A_fireprojectile("RifleClipSpawn",5,false,0,-14,0);
					A_DiscardMag();
				}
			}
			BR4R FGGGGGGHIJKLMNOP 1;
			TNT1 A 0 
			{
				A_startsound("BR45LOAD",3);
				A_reloadMag();
			}
			BR4R QRSTUVWX 1;
			Goto Ready;
	AltFire:
		BR4Z A 1
		{
			A_StartAim(3.0);
			A_SetCrosshairDX("Null");
		}
		BR4Z BC 1;
		TNT1 A 0 {if(invoker.scopeZoom == true) A_ZoomFactor(3.0,ZOOM_INSTANT); A_ZoomFactor(9.0);}
	Aim:
		TNT1 A 0 A_HandleScope(3.0,9.0);
		BR4Z D 1 Bright A_WeaponReadyDX(WRF_ALLOWRELOAD,false);
		Loop;
	AimEnd:
		BR4Z C 1
		{
			if(invoker.scopeZoom == true) A_ZoomFactor(3.0,ZOOM_INSTANT);
			A_EndAim();
			A_SetCrosshairDX("BR45Ret",2000);
		}
		BR4Z BA 1;
		Goto Ready;
	FireAim:
			TNT1 A 0 A_CheckIfMag();
			BR4Z D 1 Bright
			{
				invoker.savedzoom = player.fov;
				//console.printf("%i",invoker.savedzoom);
				player.fov = player.fov / 0.95;
				A_FireParticles(1,8);
				A_FireBattleRifle();
				A_WeaponRecoil(0.5);
				A_startsound("BR45FIRE",1,0,0.85);
				A_QuadSound();
				A_AlertMonstersDX();
				A_GunLight();
				A_TakeMag();
				invoker.burstcount++;
			}
			BR4Z D 1 Bright
			{
				player.fov = invoker.savedzoom;
				A_fireprojectile("riflecasespawnversion2",5,false,15,-7,0);
				If(invoker.mag < 1)
				{
					A_fireprojectile("RifleClipSpawn",5,false,15,-7,0);
				}
				if(invoker.burstcount > 2)
				{
					A_semiflag();
				}
			}
			BR4Z D 2 Bright;
			TNT1 A 0 A_Jumpif(invoker.burstcount < 3,"FireAim");
			BR4Z D 3 Bright;
			TNT1 A 0 {invoker.burstcount = 0;}
			TNT1 A 0 A_SemiRefire("FireAim");
			Goto Aim;
		Spawn:
			BR45 A -1;
			LOOP;
	}
	
}