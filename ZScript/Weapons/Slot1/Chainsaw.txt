class BDPChainsaw : BDPWeaponBase
{
	override void DoSpriteStain()
	{
		super.DoSpriteStain();
		StainSprites("AWB");
		StainSprites("AWF");
		StainSprites("AWG");
		StainSprites("AWS");
	}
	
	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 2;
		weapon.selectionorder 2200;
		weapon.readysound "weapons/chainsaw/idle";
		+WEAPON.AXEBLOOD
		+WEAPON.MELEEWEAPON
		+BDPWeaponBase.BLOODSTAIN
		Inventory.PickupMessage "You got the Chainsaw! Find some meat! (Slot 1)";
		Weapon.KickBack 0;
		Tag "$BDPSAW";
		Inventory.PickupSound "weapons/chainsaw/draw";
		Inventory.AltHUDIcon "CSAWA0";
	}
	States
	{
	CacheSprites:
		1AWB ABCDEFGHIJKLMN 0;
		1AWF ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
		1AWG ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		1AWS ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		2AWB ABCDEFGHIJKLMN 0;
		2AWF ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
		2AWG ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		2AWS ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		3AWB ABCDEFGHIJKLMN 0;
		3AWF ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
		3AWG ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		3AWS ABCDEFGHIJKLMNOPQRSTUVWXYZ] 0;
		Stop;
	Spawn:
		CSAW A -1;
		Stop;
	//This state is set when spawned after being thrown
	Spawnthrown:
		CSAW B 8 {A_StartSound("weapons/chainsaw/idle", 6); A_SpawnProjectile("SmokeSpawner");}
		Loop;
	Grenade: //30
		SAWS RSTU 1;
		TNT1 A 22;
		SAWS NOPQ 1;
		TNT1 A 0 A_StartSound("DSSAWZIP", 1);
		SAWS A 3;
		SAWS BCCDDEFG 1;
		TNT1 A 0 A_StartSound("weapons/chainsaw/draw", 3);
		SAWS HIJKLM 1;
		Goto Ready;
	Kick: //18
		SAWS VWXY 1;
		SAWS [Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z[ 2;
		SAWS YXWV 1;
		Goto Ready;
	SlideKick: //25
		SAWS VWXY 1;
		SAWS [Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z[Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z 2;
		SAWS [ 1;
		SAWS YXWV 1;
		Goto Ready;
	Taunt: //30
		SAWS VWXY 1;
		SAWS [Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z[Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z[Z 2;
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [ 2;
		SAWS YXWV 1;
		Goto Ready;
	Sprint:
		SAWS VWXY 1 A_SetSprites("TNT1A0");
	SprintLoop:
		SAWS [Z 2 A_SetSprites("TNT1A0");
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,-10);}
		SAWS [Z 2 A_SetSprites("TNT1A0");
		Loop;
	SprintEnd:
		SAWS YXWV 1 A_SetSprites("TNT1A0");
		Goto Ready;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("Null"); A_GiveInventory("HasCutingWeapon",1);}
		SAWS NOPQ 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("DSSAWZIP", 1);
		SAWS A 3 A_WeaponReady(WRF_NOFIRE);
		SAWS BCCDDEFG 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/chainsaw/draw", 3);
		SAWS HIJKLM 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Deselect:
		TNT1 A 0 A_TakeInventory("HasCutingWeapon",1);
		SAWS RSTU 1;
		TNT1 A 0 A_StopSound(6);
		TNT1 A 0 A_Stopsound(1);
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Fidget:
		TNT1 A 0 A_StartSound("DSSAWZIP", 1);
		SAWS A 3 A_WeaponReadyDX(WRF_NOFIRE);
		SAWS BCCD 1 A_WeaponReadyDX(WRF_NOFIRE);
	FidgetLoop:
		SAWS D 1 A_WeaponReadyDX(WRF_NOFIRE);
		TNT1 A 0 A_PressingReload("FidgetLoop");
		SAWS EFG 1 A_WeaponReadyDX(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/chainsaw/draw", 3);
		SAWS HIJKLM 1 A_WeaponReadyDX(WRF_NOFIRE);
		Goto Ready;
	Ready:
		TNT1 A 0 {A_StopSound(1); A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		SAWG ABCDEFGG 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_JumpIf(invoker.bloodStain == 0,"Ready");
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		1AWB ABCDEFGG 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 {A_StartSound("weapons/chainsaw/idle", 6); A_FireProjectile("SmokeSpawner",0,0,0,5);}
		1AWB HIJKLMNN 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	Fire:
		TNT1 A 0 A_StopSound(6);
		TNT1 A 0 A_JumpIf(invoker.sideFired,"Fire2");
		SAWG HIJ 1;
		TNT1 A 2;
	FireNoStartup:
		TNT1 A 0 A_BDPMeleeStart(300);
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1);
		SAWF AB 1 A_BDPMeleeStart(300);
		TNT1 A 0
		{
			A_StartSound("weapons/chainsaw/draw", 5);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_QuadSound;
		}
		SAWF CDE 1 A_BDPMelee(200,"MeleeAttackSaw",0,false);
		SAWF F 1 A_BDPMelee(200,"MeleeAttackSaw",0,TRUE);
		SAWF GHI 1;
		TNT1 A 0 {A_SemiFlag(); invoker.sideFired = true;}
		TNT1 A 8 A_WeaponReadyDX(WRF_NOFIRE|WRF_NOBOB);
		TNT1 A 0 A_SemiRefire("Fire2NoStartup");
	FireEnd:
		SAWG MLK 1;
		Goto Ready;
	Fire2:
		SAWG KLM 1;
		TNT1 A 2;
	Fire2NoStartup:
		TNT1 A 0 A_BDPMeleeStart(300);
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1);
		SAWF JK 1 A_BDPMeleeStart(300);
		TNT1 A 0
		{
			A_StartSound("weapons/chainsaw/draw", 5);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_Saw("", "", 1, "SSawPuff2", 0 , 100, 32);
			A_QuadSound;
		}
		SAWF LMN 1 A_BDPMelee(200,"MeleeAttackSaw",0,false);
		SAWF O 1 A_BDPMelee(200,"MeleeAttackSaw",0,TRUE);
		SAWF PQR 1;
		TNT1 A 0 {A_SemiFlag(); invoker.sideFired = false;}
		TNT1 A 8 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
		TNT1 A 0 A_SemiRefire("FireNoStartup");
		SAWG JIH 1;
		Goto Ready;
	AltFire:
		SAWG NOPPQ 1;
	FireLoop:
		TNT1 A 0 A_StartSound("weapons/chainsaw/loop",1);
		TNT1 A 0 A_QuadSound();
		SAWG RSTU 1 A_Saw("", "", 1, "SSawPuff2", SF_NOPULLIN | SF_NOTURN, 100, 32);
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_ALTATTACK,"FireLoop");
		SAWG VWON 1;
		Goto Ready;
	SpecialAction:
		SAWG HIJ 1;
		TNT1 A 2;
		SAWF CD 1;
		TNT1 A 0 A_StartSound("weapons/gswing", 1);
		TNT1 A 0 A_FireProjectile("ThrowedChainsaw", 0, 0, 0, 0);
		TNT1 A 0 A_QuadSound();
		AXEZ ABC 1
		{
			A_SetAngle(Angle+2, SPF_INTERPOLATE);
			A_SetPitch(Pitch+1, SPF_INTERPOLATE);
		}
		AXEZ D 1
		{
			A_SetAngle(Angle+1, SPF_INTERPOLATE);
			A_SetPitch(Pitch+0.5, SPF_INTERPOLATE);
		}
		AXEZ E 1
		{
			A_SetAngle(Angle+0.5, SPF_INTERPOLATE); //7,5
			A_SetPitch(Pitch+0.2, SPF_INTERPOLATE); //3,7
		}
		AXEZ E 2;
		AXEZ E 1
		{
			A_SetAngle(Angle-0.5, SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.2, SPF_INTERPOLATE);
		}
		AXEZ EEF 1
		{
			A_SetAngle(Angle-2, SPF_INTERPOLATE);
			A_SetPitch(Pitch-1, SPF_INTERPOLATE);
		}
		AXEZ G 1
		{
			A_SetAngle(Angle-1, SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.5, SPF_INTERPOLATE);
		}
		AXEZ HI 1;
		TNT1 A 1 A_jumpifinventory("PowerSpeed2",1,"Select");
		TNT1 A 0 A_GiveInventory("iJustThrowedMySaw",1);
		TNT1 A 0 A_TakeInventory("HasCutingWeapon",1);
		TNT1 A 0 A_SelectWeapon("BDPFist");
		Goto InstaDeselect;
	}
}

class SawDamageVertical : FastProjectile
{
	Default
	{
		Radius 22;
		Height 2;
		DamageType 'Saw';
		Projectile;
		+EXTREMEDEATH
		+BLOODSPLATTER
		+MTHRUSPECIES
		DamageFunction 8;
		Speed 28;
		SeeSound "none";
		DeathSound "none";
		Decal "SawVertical";
		Species "Marines";
	}
	States
	{
	Spawn:
		TNT1 A 1;
		Stop;
	Death:
		TNT1 A 11 Radius_Quake(2, 3, 0, 4, 0);
		Stop;
	XDeath:
		TNT1 A 1 Radius_Quake(2, 3, 0, 4, 0);
		TNT1 A 10 A_StartSound("CSAWFLESH", 1);
		Stop;
	}
}

class iJustThrowedMySaw : Inventory {Default{Inventory.maxAmount 1;}}

//Jesus fuckign christ this took forever to do but im proud of it -Emerald
CLASS ThrowedChainsaw : actor
{
	Default
	{
    Radius 10;
	Height 4;
	Speed 45;
	Scale 0.8;
	Damagefunction 0;
	DamageType "Saw";
	+MISSILE;
	+FORCEXYBILLBOARD;
	-NOGRAVITY;
	+THRUSPECIES;
	+BLOODSPLATTER;
	Species "Marines";
	+MTHRUSPECIES;
	//+ripper;
	+skyexplode;
	Gravity 0.9;
	Obituary "%o was cut up by a Chainsaw";
	//SeeSound "weapons/chainsaw/loop"
	Decal "SawVerticalThrown";
	//+ripper
	}
	

		
	
    Vector3 stickOfs;
    double stickAngle;
	
	int sawtimer;
	
	
	
	Override int SpecialMissileHit(Actor victim) 
		{

			if (victim && victim.bshootable && victim.health > 0 && !victim.binvulnerable)
			{
				tracer = victim;
				
				stickOfs = victim.Vec3To(self);
                    stickAngle = tracer.angle;
				
			
			}
			Else if(victim)
			{
			setstatelabel("fall");
			}
			Return -1;
		}
	Override void postbeginplay()
		{
		super.postbeginplay();
		sawtimer = 0;
		//savedangle = angle;
		}
	
	
	States
	{
	Spawn:
		CSAW B 4 
		{
		A_ALertMonsters(200);
		}
		SAWG A 0 A_startSound("weapons/chainsaw/loop",4);
		Loop;
		
		
		
	Death:
		Xdeath:
		TNT1 A 0;
		TNT1 A 0 
		{
		If(!tracer)
		{
		setstatelabel("explode");
		}
		}
		Stuck:
		CSAW BBBB 1
		{
		
		If(!tracer || tracer.health < 1 || sawtimer > 150)
		{
		If(tracer && !tracer.bnoblood)
		{
		A_startsound("misc/gibbed",24);
		}
		setstatelabel("fall");
		}
		sawtimer++;
		if(tracer) 
			{ 
			If(tracer.health > 0)
			{
			
			double angDiff = DeltaAngle(stickAngle, tracer.angle);
             if (angDiff)
             {
                stickOfs.xy = RotateVector(stickOfs.xy, angDiff);
                angle += angDiff;
             }

             SetOrigin(tracer.Vec3Offset(stickOfs.x, stickOfs.y, stickOfs.z), true);
            stickAngle = tracer.angle;
			//tracer.a_stop();
			
			//Warp(tracer,calculatingpos.x,calculatingpos.y,calculatedpos.z,savedangle,WARPF_NOCHECKPOSITION | WARPF_ABSOLUTEANGLE | WARPF_INTERPOLATE | WARPF_TOFLOOR);
			//angle = savedangle;
			}
		
			}
		}
		SAWG AA 0 
		{
		A_spawnprojectile("sawdamagevertical",0,0,0,0,0,AAPTR_TRACER);
		
		A_startSound("weapons/chainsaw/loop",4);
			If(tracer && tracer.findstate("pain") && tracer.health > 0)
				{
					tracer.setstatelabel("pain.cut");
				}
		}
		LOOP;
		
	
	Explode:
		TNT1 A 0 A_checkceiling("fall");
	    AXEG A 0 A_SpawnItemEX("Sparks");
		AXEG A 0 A_startSound("AXECLN", 6);
		TNT1 A 0 A_StopSound(4);
	    TNT1 A 0 A_ALertMonsters(200);
		TNT1 A 0 
		{	
			//We need to make this the physical chainsaw actor because of the new pickup system
			//Through the magic of zscript we can give it the same properties as the original actor
			//A_SpawnItemEx ("ChainsawStuckInWall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			let thrownchainsaw = spawn("BDPChainsaw",pos);
			If(thrownchainsaw)
			{
				//Thrown chainsaws are 3d and get stuck in walls, 
				//so we need to set their state label and disable their gravity
				thrownchainsaw.setstatelabel("SpawnThrown");
				thrownchainsaw.bnogravity = true;
				thrownchainsaw.angle = angle;
				
				//We don't want doomguy to comment on a chainsaw he's already collected,
				//So we need to set that flag.
				//I think we can't cast to DECORATE actors so I'm just casting BDPWeaponBase
				let csaw = BDPWeaponBase(thrownchainsaw);
				If(csaw)
				{
					csaw.newweapon = true;
				}
			}
			
		}
		stop;
		
	Fall:
		 //AXEG A 0 A_SpawnItemEX("Sparks");
		//AXEG A 0 A_startSound("AXECLN", 6);
		TNT1 A 0 A_StopSound(4);
	    TNT1 A 0 A_ALertMonsters(200);
		TNT1 A 0 
		{	
			//We need to make this the physical chainsaw actor because of the new pickup system
			//Through the magic of zscript we can give it the same properties as the original actor
			//A_SpawnItemEx ("ChainsawStuckInWall",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			let thrownchainsaw = spawn("BDPChainsaw",pos);
			If(thrownchainsaw)
			{
				thrownchainsaw.setstatelabel("SpawnThrown");
				thrownchainsaw.angle = angle;
			}
			
			//We don't want doomguy to comment on a chainsaw he's already collected,
			//So we need to set that flag.
			let csaw = BDPWeaponBase(thrownchainsaw);
			If(csaw)
			{
				csaw.newweapon = true;
			}
			
		}
		stop;
	
	}
}



Class MeleeAttackSaw : Actor
{
	Default
	{
		Radius 4;
		Height 2;
		Damagetype "cut";
		damagefunction (30);
		Projectile;
		speed 30;
		decal "SawHorizontal";
		+BLOODSPLATTER;
	
	}
	States
	{
		Spawn:
		TNT1 A 0 BRIGHT;
        TNT1 A 1; //A_PlaySound("weapons/gswing")
		Stop;
		Death:
			SAWG A 0 A_spawnitemex("ricochet",0,0,0,0,0,0,180);
			//SAWG A 0 A_SpawnItem("SSAwPuff")
			//SAWG A 0 A_PlaySound("DSSAWAL", 1)
			TNT1 A 1 Radius_Quake(2, 3, 0, 4, 0);
			TNT1 A 10;
			Stop;
	XDeath:
		SAWG A 0;
		TNT1 A 1 Radius_Quake(2, 3, 0, 4, 0);
		SAWG A 0 A_StartSound("CSAWFLESH", 1);
		TNT1 A 10;
		Stop;
	}

}



