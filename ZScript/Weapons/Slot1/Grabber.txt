class BDPGrabber : BrutalWeapon
{
	bool previousgravity;
	bool previousmissile;
	bool previoussolid;
	bool previousxy;
	bool previousfriend;
	
	Actor grabbedactor;
	
	int grabbedtimer;
	
	action void A_CheckGrab(double range, double ang)
	{
		Vector3 viewPos = (pos.xy, player.viewz); // player view point
		Vector3 viewDir = (AngleToVector(angle, cos(pitch)), -sin(pitch)); // player view direction
		double maxAngle = cos(ang);
		
		let it = ThinkerIterator.Create("Actor", STAT_DEFAULT);
		Actor mo;
		while (mo = Actor(it.Next()))
		{
			// get the object's bounding box
			Vector3 rel = mo.PosRelative(CurSector); // account for sector portals
			Vector3 minBox = rel - (mo.radius, mo.radius, 0);
			Vector3 maxBox = rel + (mo.radius, mo.radius, mo.height);
			
			// get the point on the box nearest the view point
			Vector3 nearest;
			nearest.x = max(minBox.x, min(viewPos.x, maxBox.x));
			nearest.y = max(minBox.y, min(viewPos.y, maxBox.y));
			nearest.z = max(minBox.z, min(viewPos.z, maxBox.z));
			
			Vector3 dir = nearest - viewPos;
			double dist = dir.Length();
			if (dist > 0)
			{
				if (dist > range) // is it in range?
					continue;
				
				if (viewDir dot (dir / dist) < maxAngle) // is it within an acceptable angle?
					continue;
			}
			if(mo.species == "marines")
				continue;
			if(mo.bshootable || mo.bmissile)
			{
				if(mo.bspawnceiling || mo.health < 1 || mo is "headkicking" || mo is "BDECGRASS" || mo is "Brutal_Bloodspot")
				{
					continue;
				}
				invoker.grabbedactor = mo;
			}
			else
			{
				continue;
			}
		// grab it
		break;
		}
	}

	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SlotPriority 3;
		Weapon.SelectionOrder 3000;
		Inventory.PickupSound "BFGREADY";
		Tag "Grabber";
		Inventory.PickupMessage "Grabber";
		Inventory.AltHUDIcon "BFUGA0";
	}
	States
	{
	Grenade: //32
		BFGS BCDE 1;
		TNT1 A 24;
		BFGS EDCB 1;
		Goto Ready;
	Kick: //16
		BFGN BCDE 1;
		BFGN F 8;
		BFGN EDCB 1;
		Goto Ready;
	AirKick: //18
		BFGN BCDE 1;
		BFGN F 10;
		BFGN EDCB 1;
		Goto Ready;
	SlideKick: //26
		BFGN BCDE 1;
		BFGN F 18;
		BFGN EDCB 1;
		Goto Ready;
	Taunt: //30
		BFGN BCDE 1;
		BFGN F 22;
		BFGN EDCB 1;
		Goto Ready;
	Ready:
		BFGN A 1 A_WeaponReadyDX();
		Loop;
	Sprint:
		BFGN BCDE 1 A_WeaponReadyDX(0,FALSE,FALSE);
	SprintLoop:
		BFGN F 1 A_KeepSprinting();
		Loop;
	SprintEnd:
		BFGN EDCB 1 A_weaponreadyDX(0,FALSE,FALSE);
		Goto ready;
	Deselect:
		BFGS ABCDE 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 A_SetCrosshairDX("PLASRet", 200,1.0);
		BFGN A 0 A_startSound("BFGREADY");
		BFGS EDCBA 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Fire:
		TNT1 A 0
		{
			A_checkgrab(200,45);
			if(invoker.grabbedactor)
			{
				invoker.previousmissile = invoker.grabbedactor.bmissile;
				invoker.previoussolid = invoker.grabbedactor.bsolid;
				invoker.previousgravity = invoker.grabbedactor.bnogravity;
				invoker.previousxy = invoker.grabbedactor.bforcexybillboard;
				invoker.previousfriend = invoker.grabbedactor.bfriendly;
				invoker.grabbedactor.giveinventory("grabbedmonsterdamage",1);
				invoker.grabbedactor.giveinventory("grabbedmonsterdamage",1);
				invoker.grabbedactor.bsolid = FALSE;
				invoker.grabbedactor.bmissile = FALSE;
				invoker.grabbedactor.bnogravity = TRUE;
				invoker.grabbedactor.bforcexybillboard = TRUE;
				invoker.grabbedactor.bfriendly = TRUE;
				invoker.grabbedactor.bmissilemore = TRUE;
				invoker.grabbedactor.bnopain = TRUE;
				invoker.grabbedactor.blookallaround = TRUE;
				invoker.grabbedactor.bdontharmspecies = FALSE;
				invoker.grabbedactor.bdoharmspecies = TRUE;
				invoker.grabbedactor.target = null;
				invoker.grabbedactor.tracer = null;
				return resolvestate("GrabLoop");
			}
			return resolvestate(null);
		}
		Goto GrabDie;
	GrabShoot:
		TNT1 A 0
		{
			A_startsound("GRBRFIRE",12);
			if(invoker.grabbedactor)
			{
				invoker.grabbedactor.bmissile = invoker.previousmissile;
				invoker.grabbedactor.bsolid = invoker.previoussolid;
				invoker.grabbedactor.bnogravity = invoker.previousgravity;
				invoker.grabbedactor.bforcexybillboard = invoker.previousxy;
				invoker.grabbedactor.bfriendly = invoker.previousfriend;
				if(invoker.grabbedactor.bmissile)
				{
					invoker.grabbedactor.giveinventory("grabberdamage",1);
					invoker.grabbedactor.pitch = invoker.owner.pitch;
					invoker.grabbedactor.angle = invoker.owner.angle;
					invoker.grabbedactor.species = "marines";
					invoker.grabbedactor.bthruspecies = true;
					invoker.grabbedactor.bmthruspecies = true;
					invoker.grabbedactor.setorigin((invoker.owner.pos.x,invoker.owner.pos.y,(invoker.owner.pos.z + (invoker.owner.height * 0.5))),FALSE);
					Invoker.grabbedactor.target = invoker.owner;
					invoker.grabbedactor.vel3dfromangle(invoker.grabbedactor.speed,invoker.grabbedactor.angle,invoker.grabbedactor.pitch);
				}
				else
				{
					invoker.A_startsound("bonecrack",13);
					Radius_Quake(12, 24, 0, 60, 0);
					invoker.grabbedactor.takeinventory("grabbedmonsterdamage",1);
					invoker.grabbedactor.angle = (invoker.owner.angle - 180);
					invoker.grabbedactor.a_changevelocity(-20,0,20,CVF_RELATIVE);
					invoker.grabbedactor.damagemobj(invoker.owner,invoker.owner,frandom(100,170),"extremepunches");
				}
			invoker.grabbedactor = null;
			}
		}
		BFGA A 1 BRIGHT A_ZoomFactor(0.96);
		BFGA B 1 BRIGHT A_FireProjectile("GreenFlareSpawn",0,0,0,0);
		TNT1 A 0 A_SetPitch(Pitch-8, SPF_INTERPOLATE);
		BFGA C 1 BRIGHT;
		BFGN A 0 A_ZoomFactor(0.98);
		BFGA DE 1 BRIGHT;
		BFGN A 0 A_ZoomFactor(1.0);
		BFGA F 1;
		BFGA FFFFGGHIJK 1 A_SetPitch(Pitch+0.8, SPF_INTERPOLATE);
		Goto Ready;
	GrabDie:
		TNT1 A 0
		{
			A_startsound("GRBRFAIL",12);
			if(invoker.grabbedactor)
			{
				invoker.grabbedactor.takeinventory("grabbedmonsterdamage",1);
				invoker.grabbedactor.bmissile = invoker.previousmissile;
				invoker.grabbedactor.bsolid = invoker.previoussolid;
				invoker.grabbedactor.bnogravity = invoker.previousgravity;
				invoker.grabbedactor.bforcexybillboard = invoker.previousxy;
				invoker.grabbedactor.bfriendly = invoker.previousfriend;
				if(invoker.grabbedactor.bmissile)
				{
					invoker.grabbedactor.vel.xy = (0,0);
					invoker.grabbedactor.vel.z = -5;
				}
				invoker.grabbedactor = null;
			}
		}
		BFGN GHIGHI 1
		{
			A_overlayoffset(overlayid(),frandom(-1,0),frandom(32,33));
		}
		Goto Ready;
	GrabLoop:
		BFGN GHIGHIGHI 1
		{
			invoker.grabbedtimer++;
			A_startsound("GRBRLOOP",12,CHANF_LOOPING);
			A_overlayoffset(overlayid(),frandom(-1,0),frandom(32,33));
			if(player.cmd.buttons & bt_altattack || invoker.grabbedtimer == 120)
			{
				invoker.grabbedtimer = 0;
				return resolvestate("GrabDie");
			}
			if(invoker.grabbedactor && invoker.grabbedactor.health > 0)
			{
				invoker.grabbedactor.reactiontime = 0;
				if(invoker.grabbedactor.target && invoker.grabbedactor.target == invoker.owner)
				{
					Invoker.grabbedactor.target = null;
				}
				int warpoffset = ((invoker.grabbedactor.radius * 0.5) + (invoker.owner.radius * 0.5));
				Vector3 ofs = invoker.owner.pos+(0,0,invoker.owner.player.viewz - invoker.owner.pos.z);
				Vector3 x, y, z;
				[x, y, z] = BDP_CoordUtil.getaxes(invoker.owner.pitch,invoker.owner.angle,invoker.owner.roll);
				vector3 newpos = (ofs+x*(32 + (invoker.grabbedactor.radius * 0.7))-z*(invoker.grabbedactor.height * 0.6));
				if(level.IsPointInLevel(newpos))
				{
					invoker.grabbedactor.SetOrigin(newpos,true);
				}
				if(invoker.grabbedactor.distance3d(invoker.owner) > 300)
				{
					return resolvestate("GrabDie");
				}
			}
			else
			{
				return resolvestate("GrabDie");
			}
				
			if(JustPressed(BT_ATTACK) && invoker.grabbedtimer > 1)
			{
				invoker.grabbedtimer = 0;
				return resolvestate("GrabShoot");
			}
			return resolvestate(null);
		}
		Loop;
	Spawn:
		BFUG A -1;
		Stop;
	}
}