class BDPShotgun : BDPWeaponBase
{
	bool emptyChamber;
	bool glassSlugs;
	
	Default
	{
		Weapon.SlotNumber 3;
		Weapon.SlotPriority 1;
		Weapon.SelectionOrder 1300;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "BDPShell";
		BDPWeaponBase.MagCapacity 9;
		Inventory.PickupMessage "You got the Shotgun! (Slot 3)";
		Inventory.PickupSound "weapons/sgpump";
		Obituary "$OB_MPSHOTGUN";
		Tag "$BDPSHOTGUN";
		Inventory.AltHUDIcon "SHTCA0";
	}
	States
	{
	Grenade: //30
		TNT1 A 0 A_EndAim();
		SHTA JKLM 1;
		TNT1 A 22;
		SHTA MLKJ 1;
		Goto Ready;
	Kick: //18
		SHTA RSTU 1;
		SHTA V 10;
		SHTA UTSR 1;
		Goto Ready;
	SlideKick: //25
		SHTA RSTU 1;
		SHTA V 17;
		SHTA UTSR 1;
		Goto Ready;
	Taunt: //30
		TNT1 A 0 A_EndAim();
		SHTA RSTU 1;
		SHTA V 22;
		SHTA UTSR 1;
		Goto Ready;
	Sprint:
		SHTA RSTU 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		SHTA V 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		SHTA UTSR 1 A_SetSprites("TNT1A0",1);
		Stop;
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_jump(256,"spawn1","spawn2","spawn3");
		LOOP;
	Spawn1:
		SHTC B 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn2:
		SHTC C 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn3:
		SHTC D 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	SpawnNoAmmo:
		SHTC A -1;
		LOOP;
	Fidget:
		TNT1 A 0 A_EndAim();
		TNT1 A 0 A_StartSound("foley/ShotgunPumpMisc", 10);
		SHTN ABCDE 1 A_WeaponReadyDX(0,false);
	FidgetSpam:
		SHTN FG 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_StartSound("foley/ShotgunPumpIn", 11);
		TNT1 A 0 A_StartSound("weapons/shotgun/pump1", 3);
		SHT0 A 0 A_JumpIf(invoker.glassSlugs,2);
		SHT6 A 0;
		#### ABCDEFFF 1 A_WeaponReadyDX(0,false);
	FidgetLoop:
		#### F 2 A_WeaponReadyDX(WRF_ALLOWRELOAD,false,fidgetstate:"FidgetLoop");
		#### A 0 A_PressingReload("FidgetLoop");
		#### G 1 A_WeaponReadyDX(0,false);
		#### A 0 A_StartSound("weapons/shotgun/pump2", 3);
		#### HI 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,false,fidgetstate:"FidgetSpam");
		TNT1 A 0 A_StartSound("foley/ShotgunPumpOut", 11);
		SHTN FED 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,false,fidgetstate:"FidgetSpam");
		TNT1 A 0 A_StartSound("foley/ShotgunPumpMisc", 10,0,0.7);
		SHTN CBA 1 A_WeaponReadyDX(0,false);
		SHTA FGHI 1 A_WeaponReadyDX(0,false);
		Goto Ready;
	Select:
		TNT1 A 1 
		{ 
			if (invoker.glassSlugs)
			{
				A_SetCrosshairDX("SLUGRet",2000);
				ChangeAmmoIcon1("SHELZ0");
			}
			else
			{
				A_SetCrosshairDX("SGUNRet", 400);
			}
			A_GunslingerMag();
		}
		TNT1 A 0 A_StartSound("weapons/sgpump");
		SHTA MLKJI 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Ready:
		SHTN A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Goto Ready;
	Deselect:
		TNT1 A 0 A_EndAim();
		SHTA IJKLM 1;
		TNT1 A 1;
		TNT1 A 0 ChangeAmmoIcon1("SHELA0");
		TNT1 A 0 A_Lower;
		Wait;
	Fire:
		SHTN A 0 A_CheckIfMag();
		TNT1 A 0 A_JumpIf(invoker.glassSlugs, "FireSlug");
		TNT1 A 0 A_JumpIf(invoker.emptyChamber,"Pump1");
		SHTA A 1 Bright
		{
			A_FireBullets(5, 5, 5, 13, "ShotgunPuff", FBF_NORANDOM,8192,"DecorativeTracer",-4);
			A_FireBullets(2.5, 2.5, 4, 13, "ShotgunPuff2", FBF_NORANDOM,400,"DecorativeTracer",-4);
			A_WeaponRecoil(6);
			A_FireParticles(12);
			A_StartSound("weapons/sg", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.96);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.emptyChamber = true;
		}
		SHTA B 1 Bright;
		TNT1 A 0 A_WeaponOffset(random(-1,1),32+random(0,1) );
		SHTN A 0 A_ZoomFactor(1.00);
		SHTA CDEW 1;
		TNT1 A 0 A_WeaponOffset(0,32);
		TNT1 A 0 A_JumpIf(GetCvar("ManualShotgunPump")==1,"Ready") ;
		TNT1 A 0 A_AutoReloadMag(1,"Pump1");
		TNT1 A 0 A_CheckIfMag(1,"Ready");
		Goto Pump1;
	FireSlug:
		TNT1 A 0 A_JumpIf(invoker.emptyChamber,"PumpingSlug");
		SHTA A 1 Bright
		{
			A_FireBullets(0.2, 0.1, 1, 95, "ShotgunPuffSlug", FBF_NORANDOM);
			for(int i = 0; i < 3; i++)
			{
				A_FireProjectile("SlugGlass", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass2", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass4", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass5", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass5", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			}
			A_FireProjectile("DecorativeNail", 0, 0, 0, 2, 0, 0);
			A_FireProjectile("SlugMetalShard", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugMetalShard", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugMetalShard2", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugGlass3", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugGlass3", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_WeaponRecoil(6);
			A_FireParticles(12);
			A_StartSound("weapons/sgSLUG", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.96);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.emptyChamber = true;
		}
		SHTA B 1 Bright;
		SHTN A 0 A_ZoomFactor(1.00);
		SHTA CDEW 1;
		TNT1 A 0 A_JumpIf(GetCvar("ManualShotgunPump")==1,"Ready") ;
		TNT1 A 0 A_AutoReloadMag(1,"Pump1");
		TNT1 A 0 A_CheckIfMag(1,"Ready");
		Goto Pumpingslug;
	Pump1:
		TNT1 A 0 A_StartSound("foley/ShotgunPumpMisc", 10);
		SHTN ABCDEFG 1;
		TNT1 A 0 A_AutoReloadMag(1,"ReloadFromPumping");
	Pump1Part2:
		TNT1 A 0 A_PressingReload("ReloadFromPumping");
		TNT1 A 0
		{
			if(invoker.glassSlugs)
			{
				A_FireProjectile("ShotCaseSlugSpawn",40,0,-8,-26);
			}
			else
			{
				A_FireProjectile("ShotCaseSpawn",40,0,-8,-26);
			}
			invoker.emptyChamber = false;
		}
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE| WRF_NOBOB);//Allows quick switch
		TNT1 A 0 A_StartSound("foley/ShotgunPumpIn", 11);
		SHTN H 1 A_StartSound("weapons/sgpump", 3);
		SHTN IJKLMN 1;
		TNT1 A 0 A_StartSound("foley/ShotgunPumpOut", 11);
		SHT0 A 0 A_JumpIf(invoker.glassSlugs,2);
		SHTN A 0;
		"####" PQ 1;
		TNT1 A 0 A_SemiFlag();
		SHTN FEDCB 1;
		TNT1 A 0 A_StartSound("foley/ShotgunPumpMisc", 10,0,0.7);
		Goto FinishPump;
	Pump2:
		TNT1 A 0 A_CheckIfMag();
		SHTN TSFG 1;
		SHTN H 1 A_StartSound("Shotgun/Pump1", 5);
		SHTN IJ 1;
		TNT1 A 0
		{
			if(invoker.glassSlugs)
			{
				A_FireProjectile("ShotCaseSlugSpawn",40,0,-8,-26);
			}
			else
			{
				A_FireProjectile("ShotCaseSpawn",40,0,-8,-26);
			}
			invoker.emptyChamber = false;
		}
	Pump2Cont:
		SHTN KLMN 1;
		TNT1 H 0 A_StartSound("weapons/sgpump", 3);
		SHT0 A 0 A_JumpIf(invoker.glassSlugs,2);
		SHTN A 0;
		"####" PQ 1;
		SHTN A 0 A_JumpIfInventory("BDPShell",1,1);
		Goto FinishPump;
		SHTN A 0 A_CheckIfMag(9,"ReloadNormally2");
		SHTN FEDCB 1;
		SHTN PQFEDCB 1 A_WeaponReady(WRF_NOFIRE|WRF_NOBOB);
		Goto FinishPump;
	Pump2Alt:
		TNT1 A 0 A_CheckIfMag();
		SHTN TSFG 1;
		SHTN H 1 A_StartSound("Shotgun/Pump1", 5);
		SHTN IJ 1;
		TNT1 A 0
		{
			if(invoker.glassSlugs)
			{
				A_FireProjectile("ShotCaseSpawn",40,0,-8,-26);
			}
			else
			{
				A_FireProjectile("ShotCaseSlugSpawn",40,0,-8,-26);
			}
			invoker.emptyChamber = false;
		}
		Goto Pump2Cont;
	PumpingSlug:
		SHTN A 6;
		TNT1 A 0 A_StartSound("foley/ShotgunPumpIn", 11);
		TNT1 A 0 A_StartSound("weapons/sgpump", 3);
		SHT9 GI 1;
		SHT9 J 1 {invoker.emptyChamber = false;}
		TNT1 A 0 A_FireProjectile("ShotCaseslugSpawn",5,0,6,-14);
		TNT1 A 0 A_StartSound("foley/ShotgunPumpOut", 11);
		SHT9 JJJ 1;
		TNT1 A 0 A_SemiFlag();
		SHT9 IHG 1;
		SHTN A 1;
		TNT1 A 0 A_SemiRefire("Fire");
		Goto Ready;
	FinishPump:
		SHTA A 0 A_Semirefire("Fire");
		SHTA FGHI 1 A_WeaponReadyDX();
		Goto Ready;
	AltFire:
		SHT8 A 1 A_StartAim(1.2);
		SHT8 B 1;
	Aim:
		SHT8 C 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	AimEnd:
		SHT8 B 1 {A_StartSound("ADSOUT"); A_ZoomFactor(1.0);}
		SHT8 A 1;
		TNT1 A 0 A_EndAim();
		Goto Ready;
	FireAim:
		TNT1 A 0 A_CheckIfMag();
		TNT1 A 0 A_JumpIf(invoker.emptyChamber,"PumpAim");
		TNT1 A 0 A_JumpIf(invoker.glassSlugs, "FireAimSlug");
		SHT8 D 1 Bright
		{
			A_FireBullets(4, 4, 5, 13, "ShotgunPuff", FBF_NORANDOM,8192,"DecorativeTracer",-4);
			A_FireBullets(2, 2, 4, 13, "ShotgunPuff2", FBF_NORANDOM,400,"DecorativeTracer",-4);
			A_WeaponRecoil(8);
			A_FireParticles(12);
			A_StartSound("weapons/sg", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(1.16);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.emptyChamber = true;
		}
		SHT8 E 1 A_ZoomFactor(1.2);
		SHT8 EFGCC 1;
		TNT1 A 0 A_JumpIf(GetCvar("ManualShotgunPump")==1,"Aim") ;
		TNT1 A 0 A_CheckIfMag(1,"Aim");
		Goto PumpAim;
	FireAimSlug:
		SHT8 D 1 Bright
		{
			A_FireBullets(0.3, 0.2, 1, 90, "ShotgunPuffSlug", FBF_NORANDOM);
			for(int i = 0; i < 3; i++)
			{
				A_FireProjectile("SlugGlass", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass2", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass4", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass5", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
				A_FireProjectile("SlugGlass5", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			}
			A_FireProjectile("DecorativeNail", 0, 0, 0, 2, 0, 0);
			A_FireProjectile("SlugMetalShard", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugMetalShard", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugMetalShard2", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugGlass3", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_FireProjectile("SlugGlass3", random(-12,12), 0, random(-1,1), -12, 0, random(-3,3));
			A_WeaponRecoil(8);
			A_FireParticles(12);
			A_StartSound("weapons/sgSLUG", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(1.16);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.emptyChamber = true;
		}
		SHT8 E 1 A_ZoomFactor(1.2);
		SHT8 EFGCC 1;
		TNT1 A 0 A_JumpIf(GetCvar("ManualShotgunPump")==1,"Aim");
		TNT1 A 0 A_CheckIfMag(1,"Aim");
		Goto PumpAim;
	PumpAim:
		SHT8 C 2;
		SHTN A 0 A_GiveInventory ("Pumping", 1);
		TNT1 A 0 A_StartSound("foley/ShotgunPumpIn", 11);
		TNT1 A 0 A_StartSound("weapons/sgpump", 3);
		SHT8 GHIIJJ 1;
		TNT1 A 0 {invoker.emptyChamber = false;}
		SHTN A 0
		{
			if(invoker.glassSlugs)
			{
				A_FireProjectile("ShotCaseSlugSpawnADS",40,0,17,-2);
			}
			else
			{
				A_FireProjectile("ShotCaseSpawnADS",40,0,17,-2);
			}
		}
		TNT1 A 0 A_StartSound("foley/ShotgunPumpOut", 11);
		SHTN A 0 A_TakeInventory("ShotgunWasEmpty", 1);
		SHT8 IHHGG 1;
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_ATTACK,"FireAim");
		Goto Aim;
	ReloadFromPumping:
		SHTN A 0 A_CheckIfMag(9,"RFP2");
		SHTN A 0 A_JumpIfInventory("BDPShell", 1, 1);
		Goto Pump2+38;
		SHTN ST 1;
		Goto InsertShellAnimation;
	DoReload:
		TNT1 A 0 A_EndAim();
		SHTN BCDEF 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
	ReloadNormally2:
		SHTN FST 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
	InsertShellAnimation:
		TNT1 A 0 A_CheckIfAmmo("BDPShell",1,"Pump2");
		SHTN UUUV 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(player.cmd.buttons & BT_USER3 || player.oldbuttons & BT_USER3,"DualWieldFromReload");
		SHT5 A 0 A_JumpIf(invoker.glassSlugs,2);
		SHTN A 0;
		"####" W 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		TNT1 A 0 A_StartSound("insertshell",5);
		TNT1 A 0 A_ReloadMag(0,true);
		SHTN X 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
		SHTN YZ 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
		SHTN "[]" 1 A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
		SHTN A 0 A_JumpIf(invoker.emptyChamber,"Pump2");
		SHTN A 0 A_CheckIfMag(9,"InsertShellAnimation");
		TNT1 A 0 A_StartSound("foley/ShotgunReloadLower", 10);
		SHTN TS 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		SHTN FEDCB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		Goto FinishPump;
	DualWieldFromReload:
		TNT1 A 0 A_JumpIfInventory("GlassSlugSelected",1,2);
		TNT1 A 0 A_Print("\cjGlass Slugs",1);
		Goto DWRCont;
		TNT1 A 0 A_Print("\cgBuckshot",1);
		Goto DWRSlugs;
	SpecialAction:
		TNT1 A 0 A_EndAim();
		TNT1 A 0 A_JumpIfInventory("BDPShell",1,2);
		TNT1 A 0 A_CheckIfMag();
		TNT1 A 0 A_StartSound("foley/ShotgunReloadRaise", 10);
		TNT1 A 0
		{
			if(invoker.glassSlugs)
			{
				A_Print("\cgBuckshot",1);
			}
			else
			{
				A_Print("\cjGlass Slugs",1);
			}
		}
		SHTN ABCDEFST 1;
		SHTN UUUV 1;
	DWRCont:
		SHTN W 0 A_JumpIf(invoker.glassSlugs,2);
		SHT5 W 0;
		"####" W 1;
		SHTN X 1 A_StartSound("insertshell");
		TNT1 A 0 
		{
			if(invoker.glassSlugs)
			{
				invoker.glassSlugs = false;
				ChangeAmmoIcon1("SHELA0");
				A_SetCrosshairDX("SGUNRet",400);
			}
			else
			{
				ChangeAmmoIcon1("SHELZ0");
				invoker.glassSlugs = true;
				A_SetCrosshairDX("SLUGRet",2000);
			}
			if(invoker.mag < 9)
			{
				A_ReloadMag(0,true);
			}
		}
		SHTN YZ 1;
		SHTN "[]" 1;
		TNT1 A 0 A_TakeInventory("StartDualWield",1);
		SHTN A 0 A_JumpIf(invoker.emptyChamber,"Pump2Alt");
		TNT1 A 0 A_StartSound("foley/ShotgunReloadLower", 10);
		SHTN TS 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		SHTN FEDCB 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
		Goto FinishPump;
	}
}

class HasSlugShells : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

// ACTOR DragonBreathPuff: HitPuff
// {
// DamageType Fire
// States
// {
// Spawn:
// 	TNT1 A 0
// 	TNT1 A 0 A_JumpIfInventory("lowgraphicsmode", 1, "DeathCheap")
// 	TNT1 AA 0 A_SpawnItemEx ("ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
// 	TNT1 A 0 A_CustomMissile ("BDExplosionparticles", 0, 0, random (0, 360), 2, random (40, 90))
// 	TNT1 A 0 A_CustomMissile ("BDExplosionparticles2", 0, 0, random (0, 360), 2, random (40, 90))
// 	TNT1 A 0 A_CustomMissile ("BDExplosionparticles3", 10, 0, random (0, 360), 2, random (40, 90))
// 	EXPL A 0 A_CustomMissile ("FlamethrowerFireParticles", 6, 0, random (0, 360), 2, random (0, 90))
// 	//FRFX ZZZ 0 BRIGHT A_CustomMissile ("BigBlackSmoke", 2, 0, random (0, 360), 2, random (0, 30))
// 	//TNT1 A 2 A_SpawnItem("BurningGroundLowDamage")
// 	TNT1 A 0 A_SpawnItemEx("TinyBurningPiece", random (-15, 15), random (-15, 15))
// 	TNT1 A 0 A_SpawnItemEx("TinyBurningPiece2", random (-35, 35), random (-35, 35))
// 	TNT1 A 0 A_SpawnItemEx("TinyBurningPiece3", random (-45, 45), random (-45, 35))
// 	TNT1 AA 20 A_CustomMissile ("BigBlackSmoke", 0, 0, random (0, 360), 2, random (40, 160))
// 	Stop
// }}

class DecorativeNail: FastProjectile
{
	Default
	{
		Projectile;
		Speed 120;
		Scale 0.6;
		Radius 6;
		Height 4;
	}
	States
	{
	Spawn:
	NAIL A 0 NoDelay;
	NAIL X 0 A_JumpIf(GetCVAR("Dox_3DNail")==1,"FLY");
	NAIL A 0;
	FLY:
	NAIL "#" 0 A_FaceMovementDirection(0,360,0);//,FMDF_INTERPOLATE)
	NAIL "#" -1;
	Stop;
	
	XDeath:
	TNT1 A 0 ;
	Stop;
	
	Death:
	NAIL X 0 A_JumpIf(GetCVAR("Dox_3DNail")==1,"Stay3D");
	//NAIL "#" 0 //A_FaceMovementDirection(0,360,0)//,FMDF_INTERPOLATE)
	NAIL A 340;
	NAIL AAAAAAAAAA 1 A_FadeOut(0.1);
	Stop;
	Stay3D:
	NAIL X 340;
	NAIL XXXXXXXXXX 1 A_FadeOut(0.1);
	Stop;
	
	}
}

class SlugGlass : Actor//: FastProjectile
{
	Default
	{
	Projectile;
	Renderstyle 'Add';
	+forcexybillboard
	+CLIENTSIDEONLY
	-NOGRAVITY
	+bounceonactors
	+NOTELEPORT
	radius 2;
	height 2;
	BounceType 'Doom';
	Speed 70;
	BounceFactor 0.5;
	WallBounceFactor 0.3;
	Scale 0.3;
	Alpha 0.9;
	+ROLLSPRITE
	+ROLLCENTER
	}
	States
	{
	Spawn:
	TNT1 A 0;
	GLSP A 0 A_SetRoll(random(0,180));
	GLSP ABCDEFGH 3 A_SetRoll(Roll+6);
	Goto Spawn+2;
	Death:
	GLSP C 200;
	Stop;
	}
}

class SlugGlass2: SlugGlass
{
	Default
	{
		Speed 80;
		BounceFactor 0.5;
		WallBounceFactor 0.2;
		Scale 0.3;
	}
}

class SlugGlass3: SlugGlass
{
	Default
	{
		Speed 95;
		BounceFactor 0.6;
		WallBounceFactor 0.2;
		Scale 0.4;
	}
}

class SlugGlass4: SlugGlass
{
	Default
	{
		Speed 110;
		BounceFactor 0.5;
		WallBounceFactor 0.1;
		Scale 0.2;
	}
}

class SlugGlass5: SlugGlass
{
	Default
	{
		Speed 100;
		BounceFactor 0.3;
		WallBounceFactor 0.3;
		Scale 0.12;
	}
}

class SlugMetalShard: SlugGlass
{
	Default
	{
		Renderstyle 'normal';
		Speed 55;
		BounceFactor 0.3;
		WallBounceFactor 0.3;
		Scale 0.09;
	}
	States
	{
	Spawn:
	JNK1 ABCDEFGH 3;
	Loop;
	Death:
	JNK1 H 200;
	Stop;
	}
}

class SlugMetalShard2: SlugMetalShard
{
	Default
	{
		Speed 60;
		BounceFactor 0.4;
		WallBounceFactor 0.3;
		Scale 0.1;
	}
}