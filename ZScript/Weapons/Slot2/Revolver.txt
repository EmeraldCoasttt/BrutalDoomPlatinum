class BDPRevolver : BrutalWeapon
{
	Default
	{
		Weapon.AmmoGive 15;
		Weapon.AmmoType "BDPClip1";
		BrutalWeapon.MagCapacity 6;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 3;
		weapon.selectionorder 3800;
		Obituary "%o was shot down by %k's revolver.";
		Inventory.PickupSound "REVDRY";
		Inventory.Pickupmessage "You got the Revolver! Yee-haw! (slot 2)";
		Tag "$BDPREVOLVER";
		Inventory.AltHUDIcon "REVOQ0";
		Inventory.MaxAmount 2;
		BrutalWeapon.Handedness 1;
		BrutalWeapon.DualWeapon "BDPDualRevolver";
	}
	States
	{
	Grenade: //32
		REVS ABC 1;
		TNT1 A 26;
		REVS CBA 1;
		Goto Ready;
	Kick: //16
		REVR CIJK 1;
		REVR L 8;
		REVR KJIC 1;
		Goto Ready;
	AirKick: //18
		REVR CIJK 1;
		REVR L 10;
		REVR KJIC 1;
		Goto Ready;
	SlideKick: //26
		REVR CIJK 1;
		REVR L 18;
		REVR KJIC 1;
		Goto Ready;
	Taunt: //30
		REVR CIJK 1;
		REVR L 22;
		REVR KJIC 1;
		Goto Ready;
	Sprint:
		REVR CIJK 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
	SprintLoop:
		REVR L 1 A_KeepSprinting(WRF_ALLOWRELOAD);
		Loop;
	SprintEnd:
		REVR KJIC 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		Goto Ready;
	Ready:
		REVO A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	Deselect:
		REVS ABC 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("REVORet",2000);A_GunSlingerMag();}
		REVO A 0 A_StartSound("REVCOCK");
		REVS CBA 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SelectFromDual:
		REVS ONML 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Fire:
		REVO A 0 A_CheckIfMag();
		REVO A 1 A_StartSound("REVFIRE1");
		REVO M 1 Bright
		{
			A_FireBullets (2, 2, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		REVO B 1 BRIGHT A_SetPitch(Pitch-4, SPF_INTERPOLATE);
		REVO A 0 A_ZoomFactor(1.0);
		REVO CDEE 1;
		TNT1 A 0 A_semiflag();
		REVO DC 1 A_SetPitch(Pitch+0.5, SPF_INTERPOLATE);
		REVO FGHIJ 1 A_SetPitch(Pitch+0.6, SPF_INTERPOLATE);
		REVO A 2;
		TNT1 A 0 A_SemiRefire("Fire","Ready");
		Goto Ready;
	AltFire:
		REVO NOP 1;
		Goto Altfire2;
	Altfire2:
		TNT1 A 0 A_CheckIfMag(1,"NoAmmo3");
		TNT1 A 0 A_StartSound("REVFIRE1");
		RE2F ABCC 1;
		RE2F D 1 Bright
		{
			A_FireBullets (4, 3, 1, 40 , "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		RE2F E 1;
		TNT1 A 0 A_SetPitch(Pitch-3, SPF_INTERPOLATE);
		TNT1 A 0 A_ZoomFactor(1.0);
		RE2F FGH 1 A_SetPitch(Pitch+1, SPF_INTERPOLATE);
		TNT1 A 0 A_Refire("Altfire2");
		REVO PON 1;
		Goto Ready;
	NoAmmo:
		REVO A 0 A_StartSound("REVFIRE1");
		REVO KLL 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("REVDRY", 4);
		TNT1 A 0 offset(0,34);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,33);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,32);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
	NoAmmo2:
		TNT1 A 0 A_AutoReloadMag();
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK) || GetPressed(BT_ALTATTACK), "NoAmmo2");
		Goto Ready;
	NoAmmo3:
		REVO A 0 A_StartSound("REVFIRE1");
		RE2F ABC 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,34) A_StartSound("REVDRY", 4);
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,33);
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,32);
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
	NoAmmo4:
		TNT1 A 0 A_AutoReloadMag();
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK) || GetPressed(BT_ALTATTACK), "NoAmmo4");
		REVO PON 1;
		Goto Ready;
	Fidget:
		RSDF JL 1 A_WeaponReadyDX(0,false,false);
		RFDF KIG 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false);
	FidgetLoop:
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_PressingReload("FidgetLoop");
		RFDF GIK 1 A_WeaponReadyDX(0,false,false);
		RSDF LJ 1 A_WeaponReadyDX(0,false,false);
		Goto Ready;
	Reload:
		REVR ACFGHIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REVR MNOPPQRSTTT 1;
		TNT1 A 0 A_DiscardMagazine();
		TNT1 AAAAAA 0 A_FireProjectile("RevolverCaseSpawn",random(70,90),0,1,-8);
		TNT1 A 0 A_StartSound("REVFLY");
		REVR WXYUUU 1;
		REV2 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		TNT1 A 0 A_ReloadMag();
		REV2 JKKKKJLMNOOO 1;
		REV3 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV3 GHIIJKLMN 1;
		Goto Ready;
	Spawn:
		REVO Q -1;
		LOOP;
	SpecialAction:
		TNT1 A 0 A_DualWield();
		Goto Ready;
	}
}