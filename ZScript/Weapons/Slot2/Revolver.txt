class BDPRevolver : BDPWeaponBase
{
	Default
	{
		Weapon.AmmoGive 15;
		Weapon.AmmoType "BDPClip1";
		BDPWeaponBase.MagCapacity 6;
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 3;
		weapon.selectionorder 3800;
		Obituary "%o was shot down by %k's revolver.";
		Inventory.PickupSound "REVDRY";
		Inventory.Pickupmessage "You got the Revolver! Yee-haw! (slot 2)";
		Tag "$BDPREVOLVER";
		Inventory.AltHUDIcon "REVOQ0";
		Inventory.MaxAmount 2;
		BDPWeaponBase.Handedness 1;
		BDPWeaponBase.DualWeapon "BDPDualRevolver";
	}
	States
	{
	Grenade: //32
		REVS ABC 1;
		TNT1 A 24;
		REVS CBA 1;
		Goto Ready;
	Kick: //18
		REVR CIJK 1;
		REVR L 10;
		REVR KJIC 1;
		Goto Ready;
	SlideKick: //25
		REVR CIJK 1;
		REVR L 17;
		REVR KJIC 1;
		Goto Ready;
	Taunt: //30
		REVR CIJK 1;
		REVR L 22;
		REVR KJIC 1;
		Goto Ready;
	Sprint:
		REVR CIJK 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		REVR L 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		REVR KJIC 1 A_SetSprites("TNT1A0",1);
		Stop;
	Ready:
		REVO A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		REVO A 1;
		REVS ABC 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("REVORet",2000);A_GunSlingerMag();}
		REVO A 0 A_StartSound("REVCOCK");
		REVS CBA 1;
		REVO A 1;
		Goto Ready;
	SelectFromDual:
		REVS ONML 1;
		Goto Ready;
	Fire:
		REVO A 0 A_CheckIfMag();
		REVO A 1 A_StartSound("REVFIRE1");
		REVO M 1 Bright
		{
			A_FireBullets (2, 2, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		REVO B 1 BRIGHT A_WeaponRecoil(10.0,0,2,0.75);
		REVO A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_semiflag();
		REVO CDEE 1;
		REVO DC 1;
		
		REVO FGHIJ 1;
		TNT1 A 0 A_SemiRefire();
		Goto Ready;
	AltFire:
		REVO NOP 1;
		Goto Altfire2;
	Altfire2:
		TNT1 A 0 A_CheckIfMag(1,"NoAmmo2");
		TNT1 A 0 A_StartSound("REVFIRE1");
		RE2F ABCC 1;
		RE2F D 1 Bright
		{
			A_FireBullets (0, 0, 1, 40 , "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-4);
			A_FireParticles(5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		RE2F E 1 A_WeaponRecoil(10.0,frandom(-4,4),2,0.75);
		TNT1 A 0 A_ZoomFactor(1.0);
		RE2F FGH 1;
		TNT1 A 0 A_Refire("Altfire2");
		REVO PON 1;
		Goto Ready;
	NoAmmo:
		REVO A 0 A_StartSound("REVFIRE1");
		REVO KLL 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("REVDRY", 4);
		TNT1 A 0 offset(0,34);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,33);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,32);
		REVO A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_AutoReloadMag();
		Goto Ready;
	NoAmmo2:
		REVO A 0 A_StartSound("REVFIRE1");
		RE2F ABC 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,34) A_StartSound("REVDRY", 4);
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,33);
		RE2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 offset(0,32);
		RE2F A 3 A_WeaponReady(WRF_NOFIRE);
		REVO PON 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_AutoReloadMag();
		Goto Ready;
	Fidget:
		RSDF JL 1 A_WeaponReadyDX(0,false);
		RFDF KIG 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false);
	FidgetLoop:
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_StartSound("REVFLIP",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_PressingReload("FidgetLoop");
		RFDF GIK 1 A_WeaponReadyDX(0,false);
		RSDF LJ 1 A_WeaponReadyDX(0,false);
		Goto Ready;
	DoReload:
		REVR ACFGHIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REVR MNOPPQRSTTT 1;
		TNT1 A 0 A_DiscardMag();
		TNT1 A 0
		{
			for(int i = 6; i > invoker.mag; i--)
			{
				switch(random(0,2))
				{
					case 0:
						A_FireProjectileDX("RevolverCasing",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 1:
						A_FireProjectileDX("RevolverCasing2",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 2:
						A_FireProjectileDX("RevolverCasing3",random(-90,90),random(65,75),-2,20,-12);
						break;
				}
			}
		}
		TNT1 A 0 A_StartSound("REVFLY");
		REVR WXYUUU 1;
		REV2 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		TNT1 A 0 A_ReloadMag();
		REV2 JKKKKJLMNOOO 1;
		REV3 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV3 GHIIJKLMN 1;
		Goto Ready;
	Spawn:
		REVO Q -1;
		LOOP;
	SpecialAction:
		TNT1 A 0 A_DualWield();
		Goto Ready;
	}
}