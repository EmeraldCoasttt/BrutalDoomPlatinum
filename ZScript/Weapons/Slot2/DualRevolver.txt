class BDPDualRevolver : BDPWeaponBase 
{
	Default
	{
		Weapon.AmmoType "BDPClip1";
		BDPWeaponBase.MagCapacity 6;
		Obituary "%o was shot down by %k's revolver.";
		Weapon.SisterWeapon "BDPRevolver";
		+WEAPON.POWERED_UP
		+BDPWeaponBase.DUALWEAPON
		Tag "$BDPREVOLVER";
		BDPWeaponBase.handedness 2;
		BDPWeaponBase.voiceacted 0;
		BDPWeaponBase.DualWeapon "BDPRevolver";
		Inventory.AltHUDIcon "REVOT0";
	}
	States
	{
	Grenade: //30
		TNT1 A 4 A_DrawDualOverlays("DeselectOverlay","DeselectOverlay",true);
		TNT1 A 22;
		TNT1 A 4 A_DrawDualOverlays("SelectOverlay","SelectOverlay",true);
		Goto Ready;
	Kick: //18
		TNT1 A 4 A_DrawDualOverlays("KickOverlay","KickOverlay",true);
		TNT1 A 10;
		TNT1 A 4 A_DrawDualOverlays("KickOverlayEnd","KickOverlayEnd",true);
		Goto Ready;
	SlideKick: //25
		TNT1 A 4 A_DrawDualOverlays("KickOverlay","KickOverlay",true);
		TNT1 A 17;
		TNT1 A 4 A_DrawDualOverlays("KickOverlayEnd","KickOverlayEnd",true);
		Goto Ready;
	KickOverlay:
		RE3F EFGH 1;
	KickOverlayLoop:
		RE3F I 1;
		Loop;
	KickOverlayEnd:
		RE3F HGFE 1;
		Goto ReadyOverlay;
	Sprint:
		TNT1 A 0 A_DrawDualOverlays("SprintOverlay","SprintOverlay",true);
		TNT1 A 4;
	SprintLoop:
		TNT1 A 1 A_DrawDualOverlays("SprintOverlay","SprintOverlay",true,true);
		Loop;
	SprintEnd:
		TNT1 A 0 A_DrawDualOverlays("SprintOverlayEnd","SprintOverlayEnd",true);
		TNT1 A 4;
		Stop;
	SprintOverlay:
		RE3F EFGH 1 A_HandleDualFiring("Fire","Altfire",true);
	SprintOverlayLoop:
		RE3F I 1 A_HandleDualFiring("Fire","Altfire",true);
		Loop;
	SprintOverlayEnd:
		RE3F HGFE 1 A_HandleDualFiring("Fire","Altfire",true);
		Goto ReadyOverlay;
	Ready:
		TNT1 A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD|WRF_NOFIRE);
		Loop;
	ReadyOverlay:
		RFDF C 1 A_HandleDualFiring("Fire","Altfire");
		Loop;
	Deselect:
		TNT1 A 7 A_DrawDualOverlays("DeselectLeft","DeselectRight",true);
		TNT1 A 0 A_Lower();
		Wait;
	DeselectLeft:
		RFDF C 2;
		REVS DEFG 1;
		Stop;
	DeselectRight:
		REVS DEFG 1;
		Stop;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("REVORet",2000); A_GunslingerMag();}
		TNT1 A 0 A_StartSound("REVCOCK");
		TNT1 A 0 A_DrawDualOverlays("SelectLeft","SelectRight",true);
		TNT1 A 6;
		Goto Ready;
	SelectLeft:
		TNT1 A 2;
		REVS GFED 1;
		Goto ReadyOverlay;
	SelectRight:
		REVS GFED 1;
		RFDF C 2;
		Goto ReadyOverlay;
	SelectFromDual:
		TNT1 A 0 A_StartSound("REVCOCK");
		REVS LMNO 1;
		TNT1 A 0 A_DrawDualOverlays("ReadyOverlay","ReadyOverlay",true);
		Goto Ready;
	Fire:
		TNT1 A 0
		{
			if(OverlayID() == 3)
				return A_CheckIfDualMag();
			else
				return A_CheckIfMag();
		}
		RFDF C 1 A_StartSound("REVFIRE1");
		RFDF B 1 BRIGHT
		{
			if(OverlayID() == 3)
			{
				A_FirePiercingBullet(30,"revolverpuff",2,2,10,-6);
				A_WeaponRecoil(15.0,4,2,0.75);
				A_FireParticles(5,12,5);
				A_TakeDualMag();
			}
			else
			{
				A_FirePiercingBullet(30,"revolverpuff",2,2,-10,-6);
				A_WeaponRecoil(15.0,-4,2,0.75);
				A_FireParticles(5,12,-5);
				A_TakeMag();
			}
			let dplr = BDPPlayerPawn(invoker.owner);
			dplr.blockFire = true;
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
		}
		RFDF A 1 Bright A_ZoomFactor(1.0);
		RFDF GDDE 1;
		RFDF FGHIJK 1;
		RFDF C 2;
		Goto ReadyOverlay;
	Altfire:
		TNT1 A 0
		{
			if(OverlayID() == 3)
				return A_CheckIfDualMag();
			else
				return A_CheckIfMag();
		}
		RFDF B 1 BRIGHT
		{
			if(OverlayID() == 3)
			{
	
				A_FirePiercingBullet(30,"revolverpuff",2,2,10,-6);
				A_WeaponRecoil(15.0,5,2,0.75);
				A_FireParticles(5,12,5);
				A_TakeDualMag();
			}
			else
			{
				A_FirePiercingBullet(30,"revolverpuff",2,2,-10,-6);
				A_WeaponRecoil(15.0,-5,2,0.75);
				A_FireParticles(5,12,-5);
				A_TakeMag();
			}
			A_FireParticles(5,12,-5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
		}
		RFDF A 1 Bright A_ZoomFactor(1.0);
		RSDF HGFE 1;
		TNT1 A 0 A_StartSound("REVFIRE1");
		RSDF D 1;
		RSDF C 1
		{
			let dplr = BDPPlayerPawn(invoker.owner);
			dplr.blockAltfire = false;
		}
		RSDF BA 1;
		RFDF GGGHJK 1;
		TNT1 A 0 A_RefireDX("Altfire",true);
		Goto ReadyOverlay;
	NoAmmo:
		TNT1 A 0 A_StartSound("REVFIRE1");
		RE3F ABB 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("REVDRY", 3);
		TNT1 A 0 A_AutoReloadMag();
		Goto ReadyOverlay;
	Fidget:
		TNT1 A 0 A_DrawDualOverlays("FidgetOverlay","FidgetOverlay",true);
		TNT1 A 5 A_WeaponReadyDX(WRF_NOFIRE,false);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		TNT1 A 4 A_WeaponReadyDX(WRF_NOFIRE,false);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		TNT1 A 4 A_WeaponReadyDX(0,false);
	FidgetLoop:
		TNT1 A 0 A_DrawDualOverlays("FidgetLoopOverlay","FidgetLoopOverlay",true);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		TNT1 A 4 A_WeaponReadyDX(WRF_NOFIRE,false);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		TNT1 A 4 A_WeaponReadyDX(WRF_NOFIRE,false);
		TNT1 A 0 A_PressingReload("FidgetLoop");
		TNT1 A 5 A_WeaponReadyDX(WRF_NOFIRE,false);
		Goto Ready;
	FidgetOverlay:
		RFDF KJIHG 1;
	FidgetLoopOverlay:
		RSDF ABCDEFGH 1;
		RFDF GHIJK 1;
		Goto ReadyOverlay;
	 DoReload:
		TNT1 A 0 {invoker.sideFired = false;}
		TNT1 A 6 A_DrawDualOverlays("DeselectLeft","DeselectRight",true);
		TNT1 A 2;
		TNT1 A 0 A_JumpIf(DualMag() == 6,"ReloadLeft");
		REV6 OPQR 1 ;
		REVR HIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REVR MNOPPQRSTTT 1;
		TNT1 A 0
		{
			A_DiscardDualMag();
			for(int i = 6; i > DualMag(); i--)
			{
				switch(random(0,2))
				{
					case 0:
						A_FireProjectileDX("RevolverCasing",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 1:
						A_FireProjectileDX("RevolverCasing2",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 2:
						A_FireProjectileDX("RevolverCasing3",random(-90,90),random(65,75),-2,20,-12);
						break;
				}
			}
		}
		TNT1 A 0 A_StartSound("REVFLY");
		REVR WXYUUU 1;
		REV2 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		TNT1 A 0 A_ReloadDualMag();
		REV2 JKKKKJLMNOOO 1;
		REV3 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV3 GHII 1;
		REV6 STUVW 1;
		TNT1 A 0 A_JumpIf(invoker.mag == 6,"FinishReload");
		TNT1 A 4;
	ReloadLeft:
		REV4 CFGHIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REV4 MNOPPQRSTTT 1;
		TNT1 A 0
		{
			A_DiscardMag();
			for(int i = 6; i > invoker.mag; i--)
			{
				switch(random(0,2))
				{
					case 0:
						A_FireProjectileDX("RevolverCasing",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 1:
						A_FireProjectileDX("RevolverCasing2",random(-90,90),random(65,75),-2,20,-12);
						break;
					case 2:
						A_FireProjectileDX("RevolverCasing3",random(-90,90),random(65,75),-2,20,-12);
						break;
				}
			}
		}
		TNT1 A 0 A_StartSound("REVFLY");
		REV4 WXYUUU 1;
		REV5 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		TNT1 A 0 A_ReloadMag();
		REV5 JKKKKJLMNOOO 1;
		REV6 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV6 GHIIJKLMN 1;
	FinishReload:
		TNT1 A 2;
		TNT1 A 6 A_DrawDualOverlays("SelectLeft","SelectRight",true);
		Goto Ready;
	SpecialAction:
		TNT1 A 0 A_EndDual();
		Goto Ready;
	}
}