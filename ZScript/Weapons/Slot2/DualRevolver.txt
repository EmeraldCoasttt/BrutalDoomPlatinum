class BDPDualRevolver : BrutalWeapon 
{
	Default
	{
		Weapon.AmmoType "BDPClip1";
		BrutalWeapon.MagCapacity 12;
		Obituary "%o was shot down by %k's revolver.";
		Weapon.SisterWeapon "BDPRevolver";
		+WEAPON.POWERED_UP
		Tag "$BDPREVOLVER";
		brutalweapon.handedness 2;
		brutalweapon.voiceacted 0;
		BrutalWeapon.DualWeapon "BDPRevolver";
		Inventory.AltHUDIcon "REVOT0";
	}
	States
	{
	Grenade: //32
		REVS DEFG 1;
		TNT1 A 24;
		REVS GFED 1;
		Goto Ready;
	Kick: //16
		RE3F EFGH 1;
		RE3F I 8;
		RE3F HGFE 1;
		Goto Ready;
	AirKick: //18
		RE3F EFGH 1;
		RE3F I 10;
		RE3F HGFE 1;
		Goto Ready;
	SlideKick: //26
		RE3F EFGH 1;
		RE3F I 18;
		RE3F HGFE 1;
		Goto Ready;
	Taunt: //30
		REDF C 30 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
		Goto Ready;
	Sprint:
		RE3F EFGH 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
	SprintLoop:
		RE3F I 1 A_KeepSprinting(WRF_ALLOWRELOAD);
		Loop;
	SprintEnd:
		RE3F HGFE 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		Goto Ready;
	Ready:
		REDF C 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	Deselect:
		REVS DEFG 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("SMGRet",2000,0.3); A_GunslingerMag();}
		TNT1 A 0 A_StartSound("REVCOCK");
		REVS GFED 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 A_StartSound("REVCOCK");
		REVS LMNO 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	 Fire:
		TNT1 A 0 A_clearoverlays(6,7);
		TNT1 A 0 A_CheckIfMag();
		TNT1 A 0 A_JumpIf(invoker.sideFired,"FireSecondGun");
		REDF A 1 A_StartSound("REVFIRE1");
		REDF B 1 BRIGHT
		{
			A_FireBullets(4, 3, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-6,-10);
			A_FireParticles(5,12,5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.sideFired = true;
		}
		REDF A 1 BRIGHT A_SetPitch(Pitch-3.6, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-3, SPF_INTERPOLATE);
		REDF A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_SemiFlag();
		REDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		REDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		REDF H 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SemiFlag();
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		REDF JK 1;
		REDF C 2;
		TNT1 A 0 A_Semirefire("Fire","Ready");
		Goto Ready;
	FireSecondGun:
		REDF A 1 A_StartSound("REVFIRE1");
		REDF M 1 BRIGHT
		{
			A_FireBullets(4, 3, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-6,10);
			A_FireParticles(5,12,-5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
			invoker.sideFired = false;
		}
		REDF L 1 BRIGHT	A_SetPitch(Pitch-3.6, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+3, SPF_INTERPOLATE);
		REDF A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_SemiFlag();
		REDF Q 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		REDF Q 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		REDF R 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		REDF TU 1;
		REDF C 2;
		TNT1 A 0 A_SemiRefire("Fire","Ready");
		Goto Ready;
	Altfire:
		TNT1 A 0 A_CheckIfMag();
		TNT1 A 0
		{
			if(invoker.sideFired)
			{
				A_Overlay(7,"AltfireOtherGun");
				A_Overlay(6,"AltFireActive2");
				A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
				invoker.sideFired = false;
			}
			else
			{
				A_Overlay(6,"AltfireOtherGun");
				A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
				A_Overlay(7,"AltFireActive");
				invoker.sideFired = true;
			}
		}
		TNT1 A 8;
		TNT1 A 0 A_Refire("AltFire2");
		Goto AltfireEnd;
	Altfire2:
		TNT1 A 0 A_CheckIfMag();
		TNT1 A 0
		{
			if(invoker.sideFired)
			{
				A_Overlay(6,"AltFireActive2");
				A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
				invoker.sideFired = false;
			}
			else
			{
				A_Overlay(7,"AltFireActive");
				invoker.sideFired = true;
			}
		}
		TNT1 A 8;
		TNT1 A 0 A_Refire("AltFire2");
	AltfireEnd:
		TNT1 A 8
		{
			if(invoker.sideFired)
			{
				A_Overlay(6,"AltfireOtherGun");
				A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
			}
			else
			{
				A_Overlay(7,"AltfireOtherGun");
			}
		}
		Goto Ready;
	AltfireOtherGun:
		RFDF C 8;
		Stop;
	AltFireActive:
		RFDF B 1 Bright
		{
			A_FireBullets(8, 6, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-6,-10);
			A_FireParticles(5,12,5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		RFDF A 1 BRIGHT A_SetPitch(Pitch-3.6, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-3, SPF_INTERPOLATE);
		REDF A 0 A_ZoomFactor(1.0);
		RSDF HGFE 1;
		TNT1 A 0 A_StartSound("REVFIRE1",13);
		RSDF DCBA 1;
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		RFDF H 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+0.75, SPF_INTERPOLATE);
		RFDF JK 1;
		Stop;
	AltFireActive2:
		RFDF B 1 BRIGHT
		{
			A_FireBullets(8, 6, 1, 60, "RevolverPuff", FBF_NORANDOM,0,"DecorativeTracer",-6,10);
			A_FireParticles(5,12,-5);
			A_StartSound("REVFIRE2", 1);
			A_StartSound("FARSHT", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.98);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		RFDF A 1 BRIGHT A_SetPitch(Pitch-3.6, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle+3, SPF_INTERPOLATE);
		REDF A 0 A_ZoomFactor(1.0);
		RSDF HGFE 1;
		TNT1 A 0 A_StartSound("REVFIRE1",13);
		RSDF DCBA 1;
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		RFDF G 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		RFDF H 1 A_SetPitch(Pitch+0.9, SPF_INTERPOLATE);
		TNT1 A 0 A_SetAngle(Angle-0.75, SPF_INTERPOLATE);
		RFDF JK 1;
		Stop;
	NoAmmo:
		TNT1 A 0 A_ClearOverlays(6,7);
		TNT1 A 0 A_JumpIf(invoker.sideFired,"NoAmmoLeft");
		TNT1 A 0 {invoker.sideFired = true;}
		TNT1 A 0 A_StartSound("REVFIRE1");
		RE3F ABB 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("REVDRY", 4);
		Goto NoAmmo2;
	NoAmmoLeft:
		TNT1 A 0 {invoker.sideFired = false;}
		TNT1 A 0 A_StartSound("REVFIRE1");
		RE3F CDD 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("REVDRY", 4);
	NoAmmo2:
		TNT1 A 0 A_AutoReloadMag();
		REDF C 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK) || GetPressed(BT_ALTATTACK), "NoAmmo2");
		Goto Ready;
	Fidget:
		TNT1 A 0 { A_Overlay(6,"Fidget2"); A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True); }
		RFDF KJIHG 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 { A_Overlay(6,"FidgetLoop2"); A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True); }
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false);
	FidgetLoop:
		TNT1 A 0 { A_Overlay(6,"FidgetLoop2"); A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True); }
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		RSDF ABCD 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_StartSound("REVFLIP2",13);
		RSDF EFGH 1 A_WeaponReadyDX(0,false,false);
		TNT1 A 0 A_PressingReload("FidgetLoop");
		RFDF GHIJK 1 A_WeaponReadyDX(0,false,false);
		Goto ready;
	Fidget2:
		RFDF KJIHG 1;
	FidgetLoop2:
		RSDF ABCDEFGH 1;
		RFDF GHIJK 1;
		STOP;
	 Reload:
		TNT1 A 0 {invoker.sideFired = false;}
		TNT1 A 0 A_ClearOverlays(6,6);
		REVS DEFG 1;
		TNT1 A 2;
		REV6 OPQR 1 ;
		REVR HIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REVR MNOPPQRSTTT 1;
		TNT1 A 0 A_DiscardMagazine();
		TNT1 AAAAAA 0 A_FireProjectile("RevolverCaseSpawn",random(70,90),0,1,-8);
		TNT1 A 0 A_StartSound("REVFLY");
		REVR WXYUUU 1;
		REV2 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		REV2 JKKKKJLMNOOO 1;
		REV3 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV3 GHII 1;
		REV6 STUVW 1;
		TNT1 A 4;
		REV4 CFGHIJKLLH 1;
		TNT1 A 0 A_StartSound("REVOPEN");
		REV4 MNOPPQRSTTT 1;
		TNT1 AAAAAA 0 A_FireProjectile("RevolverCaseSpawn",random(70,90),0,1,-8);
		TNT1 A 0 A_StartSound("REVFLY");
		REV4 WXYUUU 1;
		REV5 ABCDDDEFGHHIII 1;
		TNT1 A 0 A_StartSound("REVINSRT");
		TNT1 A 0 A_ReloadMag();
		REV5 JKKKKJLMNOOO 1;
		REV6 ABDF 1;
		TNT1 A 0 A_StartSound("REVCLOSE");
		REV6 GHIIJKLMN 1;
		TNT1 A 2;
		REVS GFED 1;
		Goto Ready;
	SpecialAction:
		TNT1 A 0 A_EndDual();
		Goto Ready;
	}
}