class BDPDualSMG : BDPWeaponBase
{
	Default
	{
		Weapon.AmmoType "BDPClip1";
		BDPWeaponBase.MagCapacity 40;
		Weapon.SisterWeapon "BDPSubmachineGun";
		Obituary "%o was shot down by %k's SMG.";
		+WEAPON.POWERED_UP
		+BDPWeaponBase.DUALWEAPON
		Tag "$BDPSMG";
		Inventory.AltHUDIcon "SMGZB0";
		BDPWeaponBase.handedness 2;
		BDPWeaponBase.voiceacted 0;
		BDPWeaponBase.DualWeapon "BDPSubmachineGun";
	}
	States
	{
	Grenade: //30
		TNT1 A 4 A_DrawDualOverlays("DeselectLeft","DeselectRight");
		TNT1 A 22;
		TNT1 A 4 A_DrawDualOverlays("SelectLeft","SelectRight");
		Goto Ready;
	KickLeft:
		LSMK ABCD 1;
	KickLeftLoop:
		LSMK E 1;
		Loop;
	KickLeftEnd:
		LSMK DCBA 1;
		Goto ReadyLeft;
	KickRight:
		RSMK ABCD 1;
	KickRightLoop:
		RSMK E 1;
		Loop;
	KickRightEnd:
		RSMK DCBA 1;
		Goto ReadyRight;
	Kick: //18
		TNT1 A 4 A_DrawDualOverlays("KickLeft","KickRight");
		TNT1 A 10;
		TNT1 A 4 A_DrawDualOverlays("KickLeftEnd","KickRightEnd");
		Goto Ready;
	SlideKick: //25
		TNT1 A 4 A_DrawDualOverlays("KickLeft","KickRight");
		TNT1 A 17;
		TNT1 A 4 A_DrawDualOverlays("KickLeftEnd","KickRightEnd");
		Goto Ready;
	Sprint:
		TNT1 A 0 A_DrawDualOverlays("KickLeft","KickRight");
		TNT1 A 4;
	SprintLoop:
		TNT1 A 1;
		Loop;
	SprintEnd:
		TNT1 A 0 A_DrawDualOverlays("KickLeftEnd","KickRightEnd");
		TNT1 A 4;
		Stop;
	Ready:
		TNT1 A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	ReadyLeft:
		LSMG A 1 A_HandleDualFiring(null,null,true);
		Loop;
	ReadyRight:
		RSMG A 1 A_HandleDualFiring(null,null,true);
		Loop;
	SpecialAction:
		TNT1 A 0 A_EndDual();
		Goto Ready;
	Deselect:
		TNT1 A 7 A_DrawDualOverlays("DeselectLeft","DeselectRight");
		TNT1 A 0 A_Lower();
		Wait;
	DeselectLeft:
		LSMG A 2;
		LSMS ABCD 1;
		Stop;
	DeselectRight:
		RSMS ABCD 1;
		Stop;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("DSMGRet", 400); A_GunslingerMag(2);}
		TNT1 A 0 A_StartSound("CLIPIN");
		TNT1 A 0 A_DrawDualOverlays("SelectLeft","SelectRight");
		TNT1 A 6;
		Goto Ready;
	SelectLeft:
		TNT1 A 2;
		LSMS DCBA 1;
		Goto ReadyLeft;
	SelectRight:
		RSMS DCBA 1;
		RSMG A 2;
		Goto ReadyRight;
	SelectFromDual:
		TNT1 A 0 {A_SetCrosshairDX("DSMGRet", 400); A_StartSound("foley/SMGDualWieldSelect");}
		SM2S JKLMNOPQ 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_DrawDualOverlays("ReadyLeft","ReadyRight");
		Goto Ready;
	FireLeft:
		TNT1 A 0 A_CheckIfMag(1,"NoAmmoLeft");
		LSMF A 1 Bright
		{
			A_FireBullets (6, 6, -1, 8, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-6,-10);
			A_FireParticles(1,8,-5);
			A_StartSound("SMGFIRE", 2);
			A_StartSound("FARSMG", 7);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight();
			A_MagClick();
			A_WeaponRecoil(1.0);
			if(!invoker.sideFired)
				A_TakeMag();
		}
		LSMF B 1
		{
			A_ZoomFactor(1.0);
			A_FireProjectileDX("PistolCasing",random(-85,-95),random(-40,-60),-12,30,-5);
		}
		LSMF C 1;
		Goto ReadyLeft;
	FireRight:
		TNT1 A 0 A_CheckIfDualMag(1,"NoAmmoRight");
		RSMF A 1 Bright
		{
			A_FireBullets (6, 6, -1, 8, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-6,10);
			A_FireParticles(1,8,5);
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			//A_GunLight();
			A_DualMagClick();
			//A_WeaponRecoil(0.5);
			if(invoker.sideFired)
				A_TakeDualMag();
		}
		RSMF B 1
		{
			A_ZoomFactor(1.0);
			A_FireProjectileDX("PistolCasing",random(-85,-95),random(-40,-60),16,30,-5);
		}
		RSMF C 1;
		Goto ReadyRight;
	NoAmmoLeft:
		TNT1 A 0 A_DryFire();
		Goto ReadyLeft;
	NoAmmoRight:
		TNT1 A 0 A_DryFire();
		Goto ReadyRight;
	Fire:
		TNT1 A 1 A_Overlay(3,"FireRight");
		TNT1 A 1 A_Overlay(2,"FireLeft");
		TNT1 A 0 { invoker.sideFired = !invoker.sideFired;}
		TNT1 A 0 A_RefireDX();
		TNT1 A 1 A_ZoomFactor(1.0);
		Goto Ready;
	DoReload:
		TNT1 A 5 {invoker.sideFired = false; A_DrawDualOverlays("DeselectLeft","DeselectRight");}
		TNT1 A 4;
		3MGR BCDE 1;
		SMGR FGGHHH 1;
		TNT1 A 0 A_DiscardDualMag(1);
		TNT1 A 0 A_StartSound("SMGREL1", 5);
		SMGR IJKLMNO 1;
		SMGR O 1 A_FireProjectileDX("EmptySMGMag",random(75,85),random(15,25),0,20,-20);
		SMGR PPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2", 5);
		TNT1 A 0 A_ReloadDualMag(1);
		SMGR XWXYZZ 1;
		SM2R ABC 1;
		3MGR DCB 1 ;
		TNT1 A 5;
		4MGR BCDEFGGHHH 1;
		TNT1 A 0 A_StartSound("SMGREL1", 5);
		4MGR IJKLMNO 1;
		TNT1 A 0 A_DiscardMag(1);
		4MGR O 1 A_FireProjectileDX("EmptySMGMag",random(-95,-105),random(15,25),0,20,-20);
		4MGR PPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2", 5);
		TNT1 A 0 A_ReloadMag(1);
		4MGR XWXYZZ 1;
		4M2R ABC 1;
		4MGR DCB 1 ;
		TNT1 A 4;
		TNT1 A 5 A_DrawDualOverlays("SelectLeft","SelectRight");
		Goto Ready;
	}
}