class BDPDualSMG : BrutalWeapon
{
	Default
	{
		Weapon.AmmoType "BDPClip1";
		BrutalWeapon.MagCapacity 80;
		Weapon.SisterWeapon "BDPSubmachineGun";
		Obituary "%o was shot down by %k's SMG.";
		+WEAPON.POWERED_UP
		Tag "$BDPSMG";
		Inventory.AltHUDIcon "SMGZB0";
		brutalweapon.handedness 2;
		brutalweapon.voiceacted 0;
		BrutalWeapon.DualWeapon "BDPSubmachineGun";
	}
	States
	{
	Grenade: //32
		SM2S ABCD 1;
		TNT1 A 24;
		SM2S DCBA 1;
		Goto Ready;
	Kick: //16
		SM2F HIJK 1;
		SM2F L 8;
		SM2F KJIH 1;
		Goto Ready;
	AirKick: //18
		SM2F HIJK 1;
		SM2F L 10;
		SM2F KJIH 1;
		Goto Ready;
	SlideKick: //26
		SM2F HIJK 1;
		SM2F L 18;
		SM2F KJIH 1;
		Goto Ready;
	Taunt: //30
		SM2F A 30 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
		Goto Ready;
	Sprint:
		SM2F HIJK 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
	SprintLoop:
		SM2F L 1 A_KeepSprinting(WRF_ALLOWRELOAD);
		Loop;
	SprintEnd:
		SM2F KJIH 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		Goto Ready;
	Ready:
		SM2F A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	SpecialAction:
		TNT1 A 0 A_EndDual();
		Goto Ready;
	Deselect:
		SM2S ABCD 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("SMGRet", 400,1.0); A_GunslingerMag(2);}
		TNT1 A 0 A_StartSound("CLIPIN");
		SM2S DCBA 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 {A_SetCrosshairDX("SMGRet", 400,1.0); A_StartSound("foley/SMGDualWieldSelect");}
		SM2S JKLMNOPQ 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Fire:
		TNT1 A 0 A_CheckIfMag();
		SM2F B 1 Bright
		{
			A_FireBullets (6, 6, -1, 8, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-6,10);
			A_FireParticles(1,8,5);
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		TNT1 A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_FireProjectile("PistolCaseSpawn",5,0,-9,-1);
		TNT1 A 0 A_SetPitch(pitch-0.4, SPF_INTERPOLATE);
		SM2F C 1 Bright
		{
			A_FireBullets (6, 6, -1, 8, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-6,-10);
			A_FireParticles(1,8,-5);
			A_StartSound("SMGFIRE", 2);
			A_StartSound("FARSMG", 7);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight();
		}
		TNT1 A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_SetAngle(angle+random(-1,1),SPF_INTERPOLATE);
		TNT1 A 0 A_FireProjectile("PistolCaseSpawn",5,0,16,-1);
		TNT1 A 0 A_SetPitch(pitch-0.4, SPF_INTERPOLATE);
		SM2F D 1;
		TNT1 A 0 A_Refire();
		SM2F E 1;
		Goto Ready;
	NoAmmo:
		TNT1 A 0 A_StartSound("weapons/empty", 3);
	NoAmmo2:
		TNT1 A 0 A_AutoReloadMag();
		SM2F A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK), "NoAmmo2");
		Goto Ready;
	Reload:
		SM2S ABCD 1;
		TNT1 A 5;
		3MGR BCDE 1;
		SMGR FGGHHH 1;
		TNT1 A 0 A_DiscardMagazine();
		TNT1 A 0 A_FireProjectile("EmptyPistolClipSpawn",-5,0,8,-4);
		TNT1 A 0 A_StartSound("SMGREL1", 5);
		SMGR IJKLMNOOPPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2", 5);
		SMGR XWXYZZ 1;
		SM2R ABC 1;
		3MGR DCB 1 ;
		TNT1 A 5;
		4MGR BCDEFGGHHH 1;
		TNT1 A 0 A_FireProjectile("EmptyPistolClipSpawn",-9,0,6,-4,-5);
		TNT1 A 0 A_StartSound("SMGREL1", 5);
		4MGR IJKLMNOOPPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2", 5);
		TNT1 A 0 A_ReloadMag(2);
		//TNT1 A 0 A_Takeinventory("HasUnloadedDualSMG",1);
		4MGR XWXYZZ 1;
		4M2R ABC 1;
		4MGR DCB 1 ;
		TNT1 A 5;
		SM2S DCBA 1;
		Goto Ready;
	}
}