class BDPSubmachineGun : BrutalWeapon
{
	Default
	{
		Weapon.AmmoGive 15;
		Weapon.AmmoType "BDPClip1";
		BrutalWeapon.MagCapacity 40;
		Obituary "%o was shot down by %k's SMG.";
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 2;
		Weapon.SelectionOrder 1800;
		Inventory.PickupSound "CLIPIN";
		Inventory.Pickupmessage "You got the Submachine Gun! (Slot 2)";
		Inventory.maxamount 2;
		Tag "$BDPSMG";
		Inventory.AltHUDIcon "SMGZA0";
		brutalweapon.handedness 1;
		BrutalWeapon.DualWeapon "BDPDualSMG";
	}
	States
	{
	Grenade: //32
		SMGS EFGH 1;
		TNT1 A 24;
		SMGS HGFE 1;
		Goto Ready;
	Kick: //16
		SMGF DEFG 1;
		SMGF H 8;
		SMGF GFED 1;
		Goto Ready;
	AirKick: //18
		SMGF DEFG 1;
		SMGF H 10;
		SMGF GFED 1;
		Goto Ready;
	SlideKick: //26
		SMGF DEFG 1;
		SMGF H 18;
		SMGF GFED 1;
		Goto Ready;
	Taunt: //30
		SM2S JKLM 1;
		SM2S X 22;
		SM2S MLKJ 1;
		Goto Ready;
	Sprint:
		SMGF DEFG 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
	SprintLoop:
		SMGF H 1 A_KeepSprinting(WRF_ALLOWRELOAD);
		Loop;
	SprintEnd:
		SMGF GFED 1 A_WeaponReadyDX(WRF_ALLOWRELOAD,FALSE,FALSE);
		Goto Ready;
	Deselect:
		SMGS EFGH 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("SMGRet", 700,0.75); A_GunSlingerMag(1);}
		TNT1 A 0 A_StartSound("CLIPIN");
		SMGS HGFE 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 A_SetCrosshairDX("SMGRet", 700,0.75);
		SM2S Q 1 A_WeaponReady(WRF_NOFIRE);
		SM2G A 0 A_StartSound("foley/SMGDualWieldDeselect");
		SM2S PONMLKJ 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Ready:
		SMGR A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	Fire:
		TNT1 A 0 A_CheckIfMag();
		SMGF A 1 Bright
		{
			A_FireBullets (3, 3, 1, 12, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-14);
			A_FireParticles(1,8);
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		TNT1 A 0 A_WeaponOffset(random(-1,1),32+random(0,1));
		TNT1 A 0 A_SetPitch(pitch-0.3, SPF_INTERPOLATE);
		TNT1 A 0 A_ZoomFactor(1.0);
		TNT1 A 0 A_SetPitch(pitch+0.15, SPF_INTERPOLATE);
		TNT1 A 0 A_FireProjectile("PistolCaseSpawn",5,0,0,-9);
		SMGF C 1;
		TNT1 A 0 A_SetPitch(pitch+0.15, SPF_INTERPOLATE);
		TNT1 A 0 A_ReFire();
		TNT1 A 0 A_WeaponOffset(0,32);
		Goto Ready;
	 FireAim:
		TNT1 A 0 A_CheckIfMag();
		SMGZ G 1 Bright
		{
			A_FireBullets (1.5, 1.5, -1, 12, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-4);
			A_FireParticles();
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(1.29);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		SMGZ I 1 A_ZoomFactor(1.3);
		TNT1 A 0 A_FireProjectile("PistolCaseSpawn",5,0,0,-9);
		TNT1 A 0 A_JumpIf(GetPressed(BT_ATTACK),"FireAim");
		Goto Aim;
	AltFire:
		SMGZ C 1 {A_StartSound("ADSIN"); A_ZoomFactor(1.3);}
		SMGZ DE 1;
		TNT1 A 0 A_StartAim();
		Goto Ready;
	Aim:
		SMGZ F 1 A_ReadyAim(true);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	AimEnd:
		SMGZ E 1 {A_StartSound("ADSOUT"); A_ZoomFactor(1.0);}
		SMGZ DC 1;
		TNT1 A 0 A_EndAim();
		Goto Ready;
	NoAmmo:
		TNT1 A 0 A_StartSound("weapons/empty", 3);
	NoAmmo2:
		TNT1 A 0 A_AutoReloadMag();
		TNT1 A 0 A_JumpIf(GetAim() > 0, "NoAmmo3");
		SMGR A 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK), "NoAmmo2");
		Goto Ready;
	NoAmmo3:
		SMGZ F 1 A_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_JumpIf(GetPressed(BT_RELOAD) || GetPressed(BT_ATTACK), "NoAmmo2");
		Goto Aim;
	Reload: 
		TNT1 A 0 A_StartSound("foley/SMGInspectMisc",10);
		SMGR BCDEFGGHHH 1;
		TNT1 A 0 A_StartSound("SMGREL1");
		TNT1 A 0 A_DiscardMagazine();
		SMGR IJKLMNO 1;
		SMGR O 1 A_FireProjectile("EmptyPistolClipSpawn",-9,0,11,-4,-5);
		SMGR PPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2");
		TNT1 A 0 A_StartSound("PISTFOL5",10);
		TNT1 A 0 A_ReloadMag(1);
		SMGR XWXYZZ 1;
		SM2R ABC 1;
		SMGR DCB 1;
		Goto Ready;
 	Spawn:
		TNT1 A 0 NoDelay A_jump(256,"spawn1","spawn2","spawn3");
		LOOP;
	Spawn1:
		SMGZ J 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn2:
		SMGZ K 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn3:
		SMGZ L 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	SpawnNoAmmo:
		SMGZ A -1;
		LOOP;
	SpecialAction:
		TNT1 A 0 A_DualWield();
		Goto Ready;
	}
}

/*ACTOR FunniSMGThrow
{
Projectile
-NOGRAVITY
Scale 0.8
Speed 5
BounceFactor 0.5
WallBounceFactor 0.3
BounceType Doom
SeeSound "weapons/largemagdrop"
DeathSound "weapons/largemagdrop"
BounceSound "weapons/largemagdrop"
+FORCEXYBILLBOARD
+ROLLSPRITE
+ROLLCENTER
	States
	{
	Spawn:
	SMGZ X 1 A_SetRoll(roll-12)
	loop
	Death:
	 // TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor")//Check Effects
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor2")//Check Effects
		SMGZ X 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Stoping:
		TNT1 A 0
		Stop
	}
}*/