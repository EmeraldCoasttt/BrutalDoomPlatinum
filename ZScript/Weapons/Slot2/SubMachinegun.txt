class BDPSubmachineGun : BDPWeaponBase
{
	Default
	{
		Weapon.AmmoGive 15;
		Weapon.AmmoType "BDPClip1";
		BDPWeaponBase.MagCapacity 40;
		Obituary "%o was shot down by %k's SMG.";
		Weapon.SlotNumber 2;
		Weapon.SlotPriority 2;
		Weapon.SelectionOrder 1800;
		Inventory.PickupSound "CLIPIN";
		Inventory.Pickupmessage "You got the Submachine Gun! (Slot 2)";
		Inventory.maxamount 2;
		Tag "$BDPSMG";
		Inventory.AltHUDIcon "SMGZA0";
		BDPWeaponBase.handedness 1;
		BDPWeaponBase.DualWeapon "BDPDualSMG";
	}
	States
	{
	Grenade: //30
		TNT1 A 0 A_EndAim();
		SMGS EFGH 1;
		TNT1 A 22;
		SMGS HGFE 1;
		Goto Ready;
	Kick: //18
		SMGF DEFG 1;
		SMGF H 10;
		SMGF GFED 1;
		Goto Ready;
	SlideKick: //25
		SMGF DEFG 1;
		SMGF H 17;
		SMGF GFED 1;
		Goto Ready;
	Taunt: //30
		TNT1 A 0 A_EndAim();
		SM2S JKLM 1;
		SM2S X 22;
		SM2S MLKJ 1;
		Goto Ready;
	Sprint:
		SMGF DEFG 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		SMGF H 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		SMGF GFED 1 A_SetSprites("TNT1A0",1);
		Stop;
	Deselect:
		TNT1 A 0 A_EndAim();
		SMGS EFGH 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower;
		Wait;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("SMGRet", 700); A_GunSlingerMag(1);}
		TNT1 A 0 A_StartSound("CLIPIN");
		SMGS HGFE 1;
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 A_SetCrosshairDX("SMGRet", 700);
		SM2G A 0 A_StartSound("foley/SMGDualWieldDeselect");
		SM2S QPONMLKJ 1;
		Goto Ready;
	Ready:
		SMGR A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		TNT1 A 0 A_AutoReloadMag();
		Loop;
	Fire:
		TNT1 A 0 A_CheckIfMag();
		SMGF A 1 Bright
		{
			A_FireBullets (3, 3, 1, 12, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-14);
			A_WeaponRecoil(0.3,0,1);
			A_FireParticles(1,8);
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight();
			A_MagClick();
			A_TakeMag();
		}
		SMGF C 1
		{
			A_FireProjectileDX("PistolCasing",random(-85,-95),random(-40,-60),0,30,-10);
			A_ZoomFactor(1.0);
			A_WeaponOffset(random(-1,1),32+random(0,1));
		}
		TNT1 A 0 A_ReFireDX();
		TNT1 A 0 A_WeaponOffset(0,32);
		Goto Ready;
	 FireAim:
		TNT1 A 0 A_CheckIfMag();
		SMGZ G 1 Bright
		{
			A_FireBullets (1.5, 1.5, -1, 12, "WeakPuff", FBF_NORANDOM,8192,"decorativetracer",-6);
			A_WeaponRecoil(0.3,0,1);
			A_FireParticles(1,8);
			A_StartSound("SMGFIRE", 1);
			A_StartSound("FARSMG", 6);
			A_AlertMonstersDX();
			A_ZoomFactor(1.29);
			A_QuadSound();
			A_GunLight();
			A_TakeMag();
		}
		SMGZ I 1 {A_FireProjectileDX("PistolCasing",random(-85,-95),random(-40,-60),0,30,-10); A_ZoomFactor(1.3);}
		TNT1 A 0 A_RefireDX("FireAim");
		Goto Aim;
	AltFire:
		SMGZ C 1 A_StartAim(1.3);
		SMGZ DE 1;
	Aim:
		SMGZ F 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	AimEnd:
		SMGZ E 1 {A_StartSound("ADSOUT"); A_EndAim();}
		SMGZ DC 1;
		Goto Ready;
	DoReload: 
		TNT1 A 0 A_EndAim();
		TNT1 A 0 A_StartSound("foley/SMGInspectMisc",10);
		SMGR BCDEFGGHHH 1;
		TNT1 A 0 A_StartSound("SMGREL1");
		TNT1 A 0 A_DiscardMag(1);
		SMGR IJKLMN 1;
		SMGR O 1 {if(invoker.mag < 2) A_FireProjectileDX("EmptySMGMag",random(75,85),random(15,25),0,20,-20);}
		SMGR OPPPQRSTUUUUV 1;
		TNT1 A 0 A_StartSound("SMGREL2");
		TNT1 A 0 A_StartSound("PISTFOL5",10);
		TNT1 A 0 A_ReloadMag(1);
		SMGR XWXYZZ 1;
		SM2R ABC 1;
		SMGR DCB 1;
		Goto Ready;
 	Spawn:
		TNT1 A 0 NoDelay A_jump(256,"spawn1","spawn2","spawn3");
		LOOP;
	Spawn1:
		SMGZ J 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn2:
		SMGZ K 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	Spawn3:
		SMGZ L 1 A_jumpifinventory("AmmoTaken",1,"SpawnNoAmmo");
		LOOP;
	SpawnNoAmmo:
		SMGZ A -1;
		LOOP;
	SpecialAction:
		TNT1 A 0 {if(invoker.aimMode > 0) {A_StartSound("ADSOUT"); A_EndAim();}}
		TNT1 A 0 A_DualWield();
		Goto Ready;
	}
}

/*ACTOR FunniSMGThrow
{
Projectile
-NOGRAVITY
Scale 0.8
Speed 5
BounceFactor 0.5
WallBounceFactor 0.3
BounceType Doom
SeeSound "weapons/largemagdrop"
DeathSound "weapons/largemagdrop"
BounceSound "weapons/largemagdrop"
+FORCEXYBILLBOARD
+ROLLSPRITE
+ROLLCENTER
	States
	{
	Spawn:
	SMGZ X 1 A_SetRoll(roll-12)
	loop
	Death:
	 // TNT1 A 0 A_SetRoll(0)
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor")//Check Effects
		TNT1 A 0 ACS_NamedExecuteAlways("BDCheckJanitor2")//Check Effects
		SMGZ X 200
		TNT1 A 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop
	Stoping:
		TNT1 A 0
		Stop
	}
}*/