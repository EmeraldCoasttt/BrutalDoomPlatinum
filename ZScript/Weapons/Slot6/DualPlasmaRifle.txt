class PlasmaCooldowncounter2 : Inventory {Default {Inventory.MaxAmount 15;}} //Check which gun will fire next
class BDPDualPlasmaRifle : BDPWeaponBase
{
	action state A_LowSprites(string nosprite, string medsprite, string highsprite, statelabel lowstate = null)
	{
		let psp = player.FindPSprite(OverlayID());
		let weap = BDPWeaponBase(FindInventory(invoker.DualWeapon));
		BDPWeaponBase target;
		if(OverlayID() == 2)
			target = weap;
		else
			target = invoker;
		if(psp)
		{
			if(target.mag < 1 || target.mag < 11 && !lowstate)
				psp.sprite = GetSpriteIndex(nosprite);
			else if(target.mag < 11 && lowstate)
				return resolvestate(lowstate);
			else if(target.mag < 31)
				psp.sprite = GetSpriteIndex(medsprite);
			else
				psp.sprite = GetSpriteIndex(highsprite);
		}
		return resolvestate(null);
	}
	
	action state A_LowFrame(int noframe, int medframe, int highframe, statelabel lowstate = null)
	{
		let psp = player.FindPSprite(OverlayID());
		let weap = BDPWeaponBase(FindInventory(invoker.DualWeapon));
		BDPWeaponBase target;
		if(OverlayID() == 2)
			target = weap;
		else
			target = invoker;
		if(psp)
		{
			if(target.mag < 1 || target.mag < 11 && !lowstate)
				psp.frame = noframe;
			else if(target.mag < 11 && lowstate)
				return resolvestate(lowstate);
			else if(target.mag < 31)
				psp.frame = medframe;
			else
				psp.frame = highframe;
		}
		return resolvestate(null);
	}
	
	Default
	{
		Weapon.AmmoType "BDPCell";
		BDPWeaponBase.MagCapacity 50;
		Weapon.SisterWeapon "BDPPlasmaRifle";
		+WEAPON.POWERED_UP
		+BDPWeaponBase.DUALWEAPON
		Tag "$BDPPLASMA";
		Inventory.AltHUDIcon "PLASB0";
		BDPWeaponBase.handedness 2;
		BDPWeaponBase.voiceacted 0;
		BDPWeaponBase.DualWeapon "BDPPlasmaRifle";
	}
	States
	{
	CacheSprites:
		3UPL ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
		2UPL ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
		Stop;
	Grenade: //30
		TNT1 A 4 A_DrawDualOverlays("DeselectOverlay","DeselectOverlay",true);
		TNT1 A 22;
		TNT1 A 4 A_DrawDualOverlays("SelectOverlay","SelectOverlay",true);
		Goto Ready;
	Kick: //18
		TNT1 A 4 A_DrawDualOverlays("KickOverlay","KickOverlay",true);
		TNT1 A 10;
		TNT1 A 4 A_DrawDualOverlays("KickOverlayEnd","KickOverlayEnd",true);
		Goto Ready;
	SlideKick: //25
		TNT1 A 4 A_DrawDualOverlays("KickOverlay","KickOverlay",true);
		TNT1 A 17;
		TNT1 A 4 A_DrawDualOverlays("KickOverlayEnd","KickOverlayEnd",true);
		Goto Ready;
	Sprint:
		TNT1 A 0 A_DrawDualOverlays("SprintOverlay","SprintOverlay",true);
		TNT1 A 4;
	SprintLoop:
		TNT1 A 1 A_DrawDualOverlays("SprintOverlay","SprintOverlay",true,true);
		Loop;
	SprintEnd:
		TNT1 A 0 A_DrawDualOverlays("SprintOverlayEnd","SprintOverlayEnd",true);
		TNT1 A 4;
		Stop;
	SprintOverlay:
		TNT1 A 0 A_LowSprites("3UPL","2UPL","DUPL","SprintOverlayLowAmmo");
		DUPL KLMN 1  {A_HandleDualFiring("Fire",null,true); A_LowSprites("3UPL","2UPL","DUPL");}
	SprintOverlayLoop:
		DUPL O 1  {A_HandleDualFiring("Fire",null,true); A_LowSprites("3UPL","2UPL","DUPL");}
		Loop;
	SprintOverlayEnd:
		TNT1 A 0  A_LowSprites("3UPL","2UPL","DUPL","SprintOverlayEndLowAmmo");
		DUPL NMLK 1 {A_HandleDualFiring("Fire",null,true); A_LowSprites("3UPL","2UPL","DUPL");}
		Goto ReadyOverlay;
	SprintOverlayLowAmmo:
		3UPL KLMNOO 1 A_HandleDualFiring("Fire",null,true);
	SprintOverlayLoopLowAmmo:
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4UPL OOOOOO 1 A_HandleDualFiring("Fire",null,true);
		3UPL OOOOOO 1 A_HandleDualFiring("Fire",null,true);
		Loop;
	SprintOverlayEndLowAmmo:
		4UPL OONMLK 1 A_HandleDualFiring("Fire",null,true);
		Goto ReadyOverlay;
	KickOverlay:
		DUPL KLMN 1 A_LowSprites("3UPL","2UPL","DUPL","KickOverlayLowAmmo");
	KickOverlayLoop:
		DUPL O 1 A_LowSprites("3UPL","2UPL","DUPL");
		Loop;
	KickOverlayEnd:
		DUPL NMLK 1 A_LowSprites("3UPL","2UPL","DUPL");
		Goto ReadyOverlay;
	KickOverlayLowAmmo:
		3UPL KLMNOO 1;
	KickOverlayLoopLowAmmo:
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4UPL OOOOOO 1;
		3UPL OOOOOO 1;
		Loop;
	KickOverlayEndLowAmmo:
		4UPL OONMLK 1;
		Goto ReadyOverlay;
	Ready:
		TNT1 A 1 A_WeaponReadyDX(WRF_ALLOWRELOAD|WRF_NOFIRE);
		Loop;
	ReadyOverlay:
		TNT1 A 0 A_HandleDualFiring("Fire");
		DUPL A 1 A_LowSprites("3UPL","2UPL","DUPL","ReadyOverlayLowAmmo");
		Loop;
	ReadyOverlayLowAmmo:
		TNT1 A 0 A_StartSound("BEP",4,0,0.3);
		4UPL AAAAAA 1 A_HandleDualFiring("Fire");
		3UPL AAAAAA 1 A_HandleDualFiring("Fire");
		Loop;
	SpecialAction:
		TNT1 A 0 A_EndDual();
		Goto Ready;
	Deselect:
		TNT1 A 6 A_DrawDualOverlays("DeselectOverlay","DeselectOverlay",true);
		TNT1 A 0 A_Lower;
		Wait;
	DeselectOverlay:
		DUPL BCDEF 1 A_LowSprites("3UPL","2UPL","DUPL");
		Stop;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("DPLSRet", 500); A_GunslingerMag;}
		DUPL A 0 A_StartSound("PLSDRAW");
		TNT1 A 0 A_DrawDualOverlays("SelectOverlay","SelectOverlay",true);
		TNT1 A 6;
		Goto Ready;
	SelectOverlay:
		DUPL FEDCB 1 A_LowSprites("3UPL","2UPL","DUPL","SelectOverlayLowAmmo");
		Goto ReadyOverlay;
	SelectFromDual:
		TNT1 A 0 {A_SetCrosshairDX("DPLSRet", 500); A_StartSound("foley/PlasmaInspectMisc",10);}
		DUPL PQRST 1 A_LowSprites("3UPL","2UPL","DUPL");
		TNT1 A 0 A_StartSound("PLSDRAW");
		TNT1 A 0 A_DrawDualOverlays("SelectOverlay",null);
		DUPL TTTTT 1 A_LowSprites("3UPL","2UPL","DUPL");
		TNT1 A 0 A_DrawDualOverlays("ReadyOverlay","ReadyOverlay",true);
		Goto Ready;
	Fire:
		TNT1 A 0
		{
			if(OverlayID() == 3)
				return A_CheckIfDualMag();
			else
				return A_CheckIfMag();
		}
		DUPL G 1 Bright
		{
			if(OverlayID() == 3)
			{
				A_FireProjectileDX("Plasma_Ball",random(-4, 4),random(-4,4),-8,0,-8,false);
				A_TakeDualMag();
			}
			else
			{
				A_FireProjectileDX("Plasma_Ball",random(-4, 4),random(-4,4),8,0,-8,false);
				A_TakeMag();
			}
			A_FirePitchDown("PLSM9", 1, 30);
			a_alertmonstersDX();
			A_QuadSound();
			A_GunLight(500,2,0,128,255);
			A_ZoomFactor(0.98);
			A_WeaponRecoil(0.7,frandom(-0.7,0.7),4);
			A_GiveInventory("PlasmaCooldowncounter",1);
		}
		DUPL H 1 Bright A_ZoomFactor(1.0);
		TNT1 A 0
		{
			let dplr = BDPPlayerPawn(invoker.owner);
			dplr.blockFire = false;
		}
		DUPL IJ 1;
		TNT1 A 0 A_RefireDX("FireOverlay");
		TNT1 A 0 A_JumpIfInventory("PlasmaCooldowncounter",5,"Cooldown");
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",15);
		Goto ReadyOverlay;
	Cooldown:
		DUPL Z 1 ;
		DUPL [\] 1;
		CHAG A 0 A_FireProjectile("SmokeSpawner11",0,0,-18,10);
		DUPL VVVWWWWWXXYY 1;
		DUPL NMLK 1 A_LowSprites("3UPL","2UPL","DUPL");
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",15);
		Goto ReadyOverlay;
	ReloadOverlay:
		TNT1 A 2;
		2LSR \ 1;
		2LSR CDEFF 1;
		PLSR G 5;
		PLSR HHIJKLL 1;
		PLSR M 4;
		PLSR NNO 1;
		PLSR PPPQRSSTTTUUVWX 1;
		2LSR YZ 1;
		2LSR [ 1;
		2LSR \ 1;
		2LSR ] 1;
		stop;
	DoReload:
		TNT1 A 0 {invoker.sideFired = false;}
		TNT1 A 6 A_DrawDualOverlays("DeselectOverlay","DeselectOverlay",true);
		TNT1 A 0 A_Overlay(6,"ReloadOverlay");
		TNT1 A 0 A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB,True);
		PLSN A 0 A_StartSound("PLREADY");
		 TNT1 AAB 1;
		 #### A 0 A_DiscardMag();
		PLSN A 0 A_FireProjectile("PlasmaCaseSpawn",-225,0,25,-15,-14);
		TNT1 CDEFF 1;
		TNT1 G 5;
		TNT1 HHIJKLL 1;
		TNT1 M 4;
		TNT1 NNO 1;
		TNT1 PPPQRSSTTTUUVWX 1;
		TNT1 A 0 A_StartSound("foley/PlasmaInspectMisc",10);
		TNT1 A 0 A_ReloadDualMag();
		TNT1 YZ 1;
		TNT1 [ 1;
		TNT1 \ 1;
		TNT1 ] 1;
		
		TNT1 A 4;
		
		TNT1 A 0 A_Overlay(6,"ReloadOverlay");
		TNT1 A 0 A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
		PLSN A 0 A_StartSound("PLREADY");
		 TNT1 AAB 1;
		PLSN A 0 A_FireProjectile("PlasmaCaseSpawn",-225,0,25,-15,-14);
		TNT1 CDEFF 1;
		TNT1 G 5;
		TNT1 HHIJKLL 1;
		TNT1 M 4;
		TNT1 NNO 1;
		TNT1 PPPQRSSTTTUUVWX 1;
		TNT1 A 0 A_StartSound("foley/PlasmaInspectMisc",10);
		TNT1 A 0 A_ReloadMag();
		TNT1 YZ 1;
		TNT1 [ 1;
		TNT1 \ 1;
		TNT1 ] 1;
		
		TNT1 A 4;
		
		TNT1 A 5 A_DrawDualOverlays("SelectOverlay","SelectOverlay",true);
		Goto Ready;
	NoAmmo:
		TNT1 A 0 A_StartSound("BEP");
		TNT1 A 0 A_AutoReloadMag();
		Goto ReadyOverlay;
	}
}