class BDPPlasmaRifle : BDPWeaponBase
{
	action state A_LowSprites(string nosprite, string medsprite, string highsprite, statelabel lowstate = null)
	{
		let psp = player.FindPSprite(OverlayID());
		if(psp)
		{
			if(invoker.mag < 1 || invoker.mag < 11 && !lowstate)
			{
				psp.sprite = GetSpriteIndex(nosprite);
			}
			else if(invoker.mag < 11 && lowstate)
			{
				return resolvestate(lowstate);
			}
			else if(invoker.mag < 31)
			{
				psp.sprite = GetSpriteIndex(medsprite);
			}
			else
			{
				psp.sprite = GetSpriteIndex(highsprite);
			}
		}
		return resolvestate(null);
	}
	
	action state A_LowFrame(int noframe, int medframe, int highframe, statelabel lowstate = null)
	{
		let psp = player.FindPSprite(OverlayID());
		if(psp)
		{
			if(invoker.mag < 1 || invoker.mag < 11 && !lowstate)
			{
				psp.frame = noframe;
			}
			else if(invoker.mag < 11 && lowstate)
			{
				return resolvestate(lowstate);
			}
			else if(invoker.mag < 31)
			{
				psp.frame = medframe;
			}
			else
			{
				psp.frame = highframe;
			}
		}
		return resolvestate(null);
	}
	
	Default
	{
		Weapon.SlotNumber 6;
		Weapon.SlotPriority 1;
		Weapon.SelectionOrder 100;
		Weapon.AmmoGive 40;
		Inventory.PickupSound "PLSDRAW";
		Weapon.AmmoType "BDPCell";
		BDPWeaponBase.MagCapacity 50;
		Tag "$BDPPLASMA";
		Inventory.PickupMessage "You got the Plasma Rifle! (Slot 6)";
		inventory.althudicon "PLASA0";
		inventory.maxamount 2;
		BDPWeaponBase.DualWeapon "BDPDualPlasmaRifle";
	}
	States
	{
	CacheSprites:
		PLSN ABCDE 0;
		2LSN KLMNOPQRST 0;
		2LSI ABCDE 0;
		3LSN KLMNOPQRST 0;
		3LSI ABCDE 0;
		4LSN KLMNOPQRST 0;
		4LSI ABCDE 0;
		3LSR AB 0;
		Stop;
	Grenade: //30
		PLSN QRST 1 A_LowSprites("3LSN","2LSN","PLSN");
		TNT1 A 22;
		PLSN TSRQ 1 A_LowSprites("3LSN","2LSN","PLSN");
		Goto Ready;
	Kick: //18
		PLSN KLMN 1 A_LowSprites("3LSN","2LSN","PLSN","KickLowAmmo");
		#### O 10;
		#### NMLK 1;
		Goto Ready;
	KickLowAmmo:
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4LSN KLMNOO 1;
		3LSN O 6;
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4LSN OONMLK 1;
		Goto ReadyLowAmmo+4;
	SlideKick: //25
		PLSN KLMN 1 A_LowSprites("3LSN","2LSN","PLSN","SlideKickLowAmmo");
		#### O 17;
		#### NMLK 1;
		Goto Ready;
	SlideKickLowAmmo:
		4LSN KLMNOO 1;
		3LSN O 6;
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4LSN O 6;
		3LSN OOONML 1;
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		4LSN K 1;
		Goto ReadyLowAmmo+2;
	Taunt: //30
		PLSN KLMN 1 A_LowSprites("3LSN","2LSN","PLSN","TauntLowAmmo");
		#### O 22;
		#### NMLK 1;
		Goto Ready;
	TauntLowAmmo: //30
		4LSN KLMNOO 1;
		3LSN O 6;
		PLSN A 0 A_StartSound("BEP",4,0,0.3) ;
		4LSN O 6;
		3LSN O 6;
		PLSN A 0 A_StartSound("BEP",4,0,0.3) ;
		4LSN OONMLK 1;
		Goto ReadyLowAmmo;
	Sprint:
		PLSN A 0 A_LowSprites("3LSN","2LSN","PLSN","SprintLowAmmo");
		#### KLMN 1 A_SetSprites("TNT1A0",1);
	SprintLoop:
		#### O 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintLowAmmo:
		4LSN KLMNOO 1 A_SetSprites("TNT1A0",1);
	SprintLoopLowAmmo:
		3LSN OOOOOO 1 A_SetSprites("TNT1A0",1);
		PLSN A 0 A_StartSound("BEP",4,0,0.3) ;
		4LSN OOOOOO 1 A_SetSprites("TNT1A0",1);
		Loop;
	SprintEnd:
		PLSN A 0 A_LowSprites("3LSN","2LSN","PLSN","SprintEndLowAmmo");
		#### NMLK 1 A_SetSprites("TNT1A0",1);
		Stop;
	SprinEndNoAmmo:
		4LSN OONMLK 1 A_SetSprites("TNT1A0",1);
		Stop;
	Fidget:
		PLSI A 0 A_LowSprites("3LSI","2LSI","PLSI");
		#### ABCDE 1 A_WeaponReadyDX(0,false);
		TNT1 A 0 A_StartSound("foley/PlasmaButtonBeep",10,0,0.9);
		TNT1 A 0 A_StartSound("BEPBEP");
		PLSI FGG 1 A_WeaponReadyDX(0,false);
		PLSI HIIIF 1 A_WeaponReadyDX(0,false);
		PLSI A 0 A_LowSprites("3LSI","2LSI","PLSI");
		#### EDCBA 1 A_WeaponReadyDX(0,false);
		PLSN A 0 A_TakeInventory("Reloading",1);
		goto ready;
	Ready:
		PLSN A 0 A_ClearOverlays(6,6);
		PLSN A 0 A_TakeInventory("PlasmaCooldowncounter",15);
		PLSN A 0 A_LowFrame(3,1,0,"ReadyLowAmmo");
		PLSN # 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	ReadyLowAmmo:
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		PLSN CCCCCCDDDDDD 1 A_WeaponReadyDX(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		PLSN A 0 A_LowSprites("3LSN","2LSN","PLSN");
		#### PQRS 1;
		TNT1 A 1;
		TNT1 A 0 A_Lower();
		Wait;
	DeselectSecondGun:
		DUPL A 0 A_LowSprites("3UPL","2UPL","DUPL");
		#### A 0;
		#### BCDEF 1;
		stop;
	Select:
		TNT1 A 1 {A_SetCrosshairDX("PLASRet", 1000); A_GunslingerMag();}
		TNT1 A 0 A_StartSound("PLSDRAW");
		PLSN A 0 A_LowSprites("3LSN","2LSN","PLSN");
		#### SRQP 1;
		Goto Ready;
	SelectFromDual:
		TNT1 A 0 {A_SetCrosshairDX("PLASRet", 1000); A_StartSound("foley/PlasmaInspectMisc",10);}
		TNT1 A 0 A_OverlayFlags(6,PSPF_ADDWEAPON|PSPF_ADDBOB|PSPF_FLIP|PSPF_MIRROR,True);
		TNT1 A 0 A_Overlay(6,"DeselectSecondGun");
		3UPL C 0 A_CheckIfMag(11,3);
		2UPL B 0 A_CheckIfMag(31,2);
		DUPL A 0;
		#### A 0;
		#### TTTTTSRQP 1 A_WeaponReady(WRF_NOFIRE);
		Goto Ready;
	Fire:
		PLSN A 0 A_CheckIfMag();
		PLSF A 1 Bright
		{
			A_FireProjectileDX("Plasma_Ball", frandom(0.3,-0.3),frandom(-0.3,0.3), 0, 0, -7);
			A_FireProjectile ("BluePlasmaParticleWeapon", random(-11,11), 0, -1, -3, 0, random(-7,14));
			A_FireProjectile ("BluePlasmaParticleWeapon", random(-11,11), 0, -1, -3, 0, random(-7,14));
			A_FirePitchDown("PLSM9", 1, 15);
			A_FireProjectile ("PlasmaFlareSpawn", 0, 0, 0, 2);
			A_WeaponRecoil(0.3);
			A_AlertMonstersDX();
			A_ZoomFactor(0.99);
			A_QuadSound();
			A_GunLight(500,2,0,128,255);
			A_TakeMag();
		}
		PLSF B 1 Bright
		{
			A_GunLight(500,2,0,128,255);
			A_GiveInventory("PlasmaCooldowncounter",1);
			A_ZoomFactor(1.0);
		}
		PLSF C 1 A_WeaponOffset(random(-1,1),32+random(0,1));
		PLSF D 1;
		TNT1 A 0 A_WeaponOffset(0,32);
		TNT1 A 0 A_RefireDX();
		TNT1 A 0 A_JumpIfInventory("PlasmaCooldowncounter",5,"Cooldown");
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",15);
		Goto Ready;
	Cooldown:
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",15);
		TNT1 A 0 A_StartSound("PLSCOOL", 3);
		PLSN EFGHI 1;
		TNT1 A 0 A_FireProjectile("SmokeSpawner11",0,0,-8,10);
		PLSN JJUUVWX 1;
		3LSF C 0 A_CheckIfMag(11,3);
		2LSF B 0 A_CheckIfMag(31,2);
		PLSF A 0;
		#### A 0;
		#### [ 1;
		#### ZYXW 1;
		PLSN A 0 A_TakeInventory("PlasmaCooldowncounter", 15);
		Goto Ready;
	StartIdleOverlay:
		PLSN R 0 A_CheckIfMag(11,"IdleRedOverlay");
		PLSN R 0 A_CheckIfMag(31,"IdleYellowOverlay");
		stop;
	AltReadyOverlay:
		PLSN ] 1 BRIGHT;
		loop;
	IdleYellowOverlay:
		PLSN Z 1 BRIGHT;
		loop;
	IdleRedOverlay:
		PLSN A 0 A_StartSound("BEP",4,0,0.3);
		PLSN [\ 6 BRIGHT;
		loop;
// Charging up secondary ------------------------------------------------------------
	AltFire:
		PLSN A 0 A_CheckIfMag(10);
		TNT1 A 0 A_SetCrosshairDX("PLS2Ret", 500);
		TNT1 A 0 A_Overlay(6,"StartIdleOverlay");
		TNT1 A 0 A_OverlayFlags(6, PSPF_ADDWEAPON|PSPF_ADDBOB, true);
		PLSN A 0 A_StartSound("PLSCHARG",1);
	AltFireLoop:
		PLSF IJKL 1 Bright A_GunLight(200,2,0,128,255);
		TNT1 A 0 A_JumpIfInventory("PlasmaCooldownCounter",10,"ContinueAlt");
		TNT1 A 0 A_TakeMag();
		TNT1 A 0 A_GiveInventory("PlasmaCooldowncounter",1);
		TNT1 A 0 A_ReFireDX("AltFireLoop",true);
		TNT1 A 0 A_StartSound("PLSCOOL", 1);
	AltFireUnloadLoop:
		TNT1 A 0 A_WeaponOffset(0,32)	;
		TNT1 A 0 A_JumpIf(CountInv("PlasmaCooldowncounter")==0,"EndAlt");
		PLSF IJKL 1 Bright A_GunLight(200,2,0,128,255);
		TNT1 A 0 A_TakeMag();
		TNT1 A 0 A_takeInventory("PlasmaCooldowncounter",1);
		loop;
	EndAlt:
		TNT1 A 0 A_SetCrosshairDX("PLASRet", 1000);
		Goto Ready;

// Holding Charge ------------------------------------------------------------

	ContinueAlt:
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",666);
		TNT1 A 0 A_Overlay(6,"AltReadyOverlay");
		PLSF MNOP 1
		{
			A_GunLight(300,2,0,128,255);
			if (CountInv("Reloading") == 1) { return resolvestate("Reload"); }
			A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			A_WeaponOffset( random(-2,2),32+random(-2,2) );
			A_StartSound("PLSFULL", 6, CHANF_LOOPING, 1, 0.8);
			return resolvestate(null);
		}
		PLSN A 0 A_ReFireDX("ContinueAlt",true);
	
// Let Fly --------------------------------------------------------------------------

		PLSN A 0 A_StopSound(6);
		PLSN A 0 A_ZoomFactor(0.9);
		TNT1 A 0 A_WeaponOffset(0,32);
		TNT1 A 0 A_ClearOverlays(6,6);
		TNT1 AAAAAAAA 0 A_FireProjectile ("BluePlasmaParticleWeapon", random(-19,19), 0, -1, 0, 0, random(-9,9));
		SHTZ A 0 
		{
		A_Recoil(3*cos(pitch));
			if(momZ != 0)
			{
			ThrustThingZ(0,pitch/2,0,1);
			}
		else
			{
			ThrustThingZ(0,pitch/3,0,1);
			}
		}
		PLSN A 0 A_StopSound(6);
		PLSN A 0 A_ZoomFactor(1.0);
		#### A 0 A_QuadSound();
		PLSN AAAAAAAAAAAA 0 A_FireProjectile("PlasmaBall75", frandom(-8,8), 0, 0, -5, 0, random(-2,2));
		PLSN A 0 A_FireProjectile("PlasmaFlareSpawn",-5,0,0,0);
		PLSN A 0 A_TakeAmmo("DoublePlasmaAmmo",8);
		PLSF A 0 A_StartSound("PLSALTFR",1);
		PLSN A 0 A_WeaponRecoil(8);
		PLSF AB 1 A_GunLight(500,2,0,128,255);
		PLSF QRSTU 1;
		TNT1 A 0 A_CheckIfMag(1,"ContinueAltNoAmmo");
		TNT1 A 0 A_StartSound("PLSCOOL", 3);
		PLSN JJUUVVWX 4 A_FireProjectile("SmokeSpawner11",0,0,-8,10);
		PLSF A 0 A_LowSprites("3LSF","2LSF","PLSF");
		#### [ 1;
		#### ZYXW 1;
		TNT1 A 0 A_SetCrosshairDX("PLASRet", 1000);
		Goto Ready;
	ContinueAltNoAmmo:
		TNT1 A 0 A_StopSound(1);
		3LSF \\\\\\\\ 4 A_FireProjectile("SmokeSpawner11",0,0,-8,10);
		3LSF [ 1;
		3LSF ZYXW 1;
		Goto Ready;
	Spawn:
		PLAS A -1;
		Stop;
	NoAmmo:
		TNT1 A 0 A_Dryfire("BEPBEP");
		Goto Ready;
	DoReload:
		PLSN A 0 A_ClearReFire();
		PLSN A 0 A_StartSound("PLREADY");
		PLSR A 0 A_LowSprites("3LSR","2LSR","PLSR");
		#### AAB 1;
		#### A 0 A_DiscardMag;
		PLSN A 0 A_FireProjectile("PlasmaCaseSpawn",-225,0,25,-15,-14);
		PLSR CDEFF 1;
		PLSR G 5;
		PLSR HHIJKLL 1;
		PLSR M 4;
		TNT1 A 0 A_ReloadMag();
		PLSR PPPQRSSTTTUUVWX 1;
		XXXX X 0 A_StartSound("foley/PlasmaInspectMisc",10);
		PLSR YZ 1;
		PLSR [ 1;
		PLSR \ 1;
		PLSR ] 1;
		PLSN A 0 A_Takeinventory("Reloading",1);
		Goto Ready;
	SpecialAction:
		TNT1 A 0 A_DualWield();
		Goto Ready;
	/*Swapweaponspecials:
	TNT1 A 0 A_takeinventory("swapriflespecial",1)
	3LSI C 0 A_CheckIfMag(11,3)
	2LSI B 0 A_CheckIfMag(31,2)
	PLSI A 0
	#### A 0
	#### ABCDE 1
	#### A 0 
	{
	if(Countinv("NeedlerSelected")==1)
			{
			A_Print("\cyCharge altfire",2);
			//A_SetCVAR("bd_needlegun",false);
			A_StartSound("BEPBEP");
			A_takeinventory("needlerselected",1);
			}
		else
			{
			A_Print("\cgNeedlegun altfire",2);
			//A_SetCVAR("bd_needleGun",true);
			A_StartSound("NDLRON");
			A_giveinventory("needlerselected",1);
			}
	}
	PLSI FGG 1
	PLSI HIIIF 1
	3LSI C 0 A_CheckIfMag(11,3)
	2LSI B 0 A_CheckIfMag(31,2)
	PLSI A 0
	#### A 0
	#### EDCBA 1
	Goto ready
	Needlegun:
		//PLSN A 0 A_CheckReload
		PLSN A 0 A_JumpIfInventory("Reloading",1,"Reload")
		PLSN A 0 A_CheckIfAmmo("PlasmaAmmo")
		TNT1 AAAA 0
		TNT1 A 0 A_GunLight(500,2,255,0,225)
		#### A 0 A_QuadSound
		PLSN A 0 A_StartSound("NDLRFIRE", 1)
		//PLSN A 0 A_FireProjectile("PlasmaFlareSpawn", 0, 0, 0, 0)
		PLSN A 0 A_TakeAmmo("DoublePlasmaAmmo", 1)
		PLSN A 0 A_JumpIfInventory("DMgame", 1, "FireDM")
		PLSN A 0 A_ZoomFactor(0.99)
		PLSN A 0 A_SetPitch(pitch-0.3, SPF_INTERPOLATE)
		//TNT1 A 0 A_FireProjectile ("PlasmaFlareSpawn", 0, 0, 0, 2)
		//TNT1 AA 0 A_FireProjectile ("BluePlasmaParticleWeapon", random(-11,11), 0, -1, -3, 0, random(-7,14))
		"----" A 0 { If(GetCVAR("BD_MagGoClicky")==1) { if(CountInv("PlasmaAmmo")<=8) { A_StartSound("MagClick", 7); }}}
		PLSF E 1 BRIGHT A_FireProjectile("Needle", frandom(3,-3), 1, 0, -14, 0, frandom(3,-3))
		TNT1 A 0 A_GiveInventory("PlasmaCooldowncounter",1)
		PLSN A 0 A_TakeAmmo("PlasmaAmmo",1)
		PLSN A 0 A_ZoomFactor(1.0)
		PLSN A 0 A_SetPitch(pitch+0.3, SPF_INTERPOLATE)
		PLSF F 1 BRIGHT
		#### A 0 A_WeaponOffset(random(-1,1),32+random(0,1) )
		
		//PLST C 1
		PLSF G 1
		#### A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_ReFire
		TNT1 A 0 A_JumpIfInventory("PlasmaCooldowncounter",15,"Cooldown")
		TNT1 A 0 A_TakeInventory("PlasmaCooldowncounter",15)
		PLSF H 1
		Goto Ready*/
	}
}

class FlareVT : VisualThinker
{
	int lifetime;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		flags = SPF_FULLBRIGHT;
		SetRenderStyle(STYLE_Add);
	}
	
	override void Tick()
	{
		if(lifetime < 1)
		{
			Destroy();
		}
		lifetime--;
		Super.Tick();
	}
}

class BlueFlareVT : FlareVT
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		texture = TexMan.CheckForTexture(random(0,1) == 1 ? 'LENBA0' : 'LENBB0');
		scale = (0.4,0.4);
		alpha = 0.4;
	}
}

class BlueFlareSmallVT : FlareVT
{
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		texture = TexMan.CheckForTexture(random(0,1) == 1 ? 'LENBA0' : 'LENBB0');
		scale = (0.13,0.13);
		alpha = 0.3;
	}
}


class Plasma_Ball: FastProjectile
{
	action void A_SpawnFlare(class<VisualThinker> flare, int lifetime)
	{
		let thonk = FlareVT(level.SpawnVisualThinker(flare));
		if(thonk)
		{
			thonk.pos = self.pos - self.vel;
			thonk.vel = self.vel;
			thonk.lifetime = lifetime;
		}
	}
	Default
	{
		Radius 10;
		Height 2;
		Speed 60;
		DamageFunction 40;
		DamageType 'Plasma';
		Decal "SmallerScorch";
		Projectile;
		+RANDOMIZE
		+SPECTRAL
		-NOBLOCKMAP
		+NOBLOOD
		+NORADIUSDMG
		+THRUSPECIES
		+MTHRUSPECIES
		+FORCEXYBILLBOARD
		Species "Marines";
		Health 5;
		Scale 1.0;
		renderstyle 'ADD';
		alpha 0.99;
		DeathSound "weapons/plasmax";
		//SeeSound "PLSM9"
		SeeSound "None";
		Obituary "$OB_MPPLASMARIFLE";
	}
	States
	{
	Spawn:
		PLSS AB 1 NoDelay Bright A_SpawnFlare("BlueFlareSmallVT",1);
		Loop;
	Death:
		PLSN A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360));
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360));
		TNT1 A 1; //A_Explode(5,50,0)
		PLSE ABC 2 Bright A_SpawnFlare("BlueFlareVT",2);
		PLSE DE 2 Bright A_SpawnFlare("BlueFlareVT",2);
		TNT2 AAA 9 A_CustomMissile ("PlasmaSmoke", 1, 0, random (0, 360), 2, random (0, 160));
		Stop;
	XDeath:
		PLSN A 0 A_CustomMissile ("BluePlasmaFire", 0, 0, random (0, 360), 2, random (0, 360));
		TNT1 AAAAA 0 A_CustomMissile ("BluePlasmaParticle", 0, 0, random (0, 360), 2, random (0, 360));
		TNT1 A 1 A_Explode(5,50,0);
		PLSE ABC 2 Bright A_SpawnFlare("BlueFlareVT",2);
		PLSE DE 2 Bright A_SpawnFlare("BlueFlareVT",2);
		Stop;
	}
}


class PlasmaBall75: Plasma_Ball
{
	Default
	{
		DamageFunction 50;
		SeeSound "PLSM9";
	}
}

class PlasmaBall99: Plasma_Ball
{
	Default
	{
		DamageFunction 60;
		SeeSound "PLSM9";
	}
}

class PlasmaCooldowncounter: Inventory 
{
	Default
	{
		Inventory.MaxAmount 15;
	}
}