class WeaponDropper : Actor
{
	name weaponToDrop;
	property WeaponToDrop : weaponToDrop;
	
	Default
	{
		Radius 0;
		Height 0;
		+THRUACTORS
	}
	States
	{
		Spawn:
		TNT1 A 0 NoDelay
		{
			if(random(0,9) == 0)
			{
				if(random(0,1) == 0)
				{
					A_CustomMissile("DroppedFragGrenade", 20, 0, random (0, 360), 2, random (30, 50));
				}
				else
				{
					A_CustomMissile("DroppedPipeBomb", 20, 0, random (0, 360), 2, random (30, 50));
				}
			}
			A_CustomMissile(WeaponToDrop, 20, 0, random (0, 360), 2, random (30, 50));
		}
		Stop;
	}
}

class DroppedWeapon : Actor
{
	Default
	{
		speed 6;
		health 1;
		radius 16;
		height 4;
		Gravity 0.9;
		damage 0;
		XScale 0.7;
		YScale 0.8;
		+ROLLSPRITE
		+ROLLCENTER
		+THRUACTORS
		+MISSILE
		+NOTELEPORT
		+NOBLOCKMAP
		+BLOODLESSIMPACT 
		+FORCEXYBILLBOARD
		+NODAMAGETHRUST
		+MOVEWITHSECTOR
		Mass 1;
		BounceType "Doom";
		BounceFactor 0.4;
		WallBounceFactor 0.2;
	}
	action void A_SpawnDroppedWeapon(name weapontype, int ammonum)
	{
		Weapon weap2drop = Weapon(Spawn(weapontype,pos));
		if (weap2drop)
		{
			weap2drop.ammoGive1 = ammonum;
		}
	}
}
class PistolDrop : WeaponDropper {Default {WeaponDropper.WeaponToDrop "DroppedPistol";}}
class RifleDrop : WeaponDropper {Default {WeaponDropper.WeaponToDrop "DroppedRifle";}}
class ShotgunDrop : WeaponDropper { Default {WeaponDropper.WeaponToDrop "DroppedShotgun";}}
class SMGDrop : WeaponDropper {Default {WeaponDropper.WeaponToDrop "DroppedSMG";}}
class MinigunDrop : WeaponDropper {Default {WeaponDropper.WeaponToDrop "DroppedMinigun";}}

class DroppedRifle : DroppedWeapon
{
	States
	{
	Spawn:
		RIFL A 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnDroppedWeapon("Rifle",15);
		Stop;
	}
}

class DroppedMinigun : DroppedWeapon
{
	States
	{
	Spawn:
		MGUN A 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnDroppedWeapon("Minigun",30);
		Stop;
	}
}

class DroppedShotgun : DroppedWeapon 
{
	States
	{
	Spawn:
		SHTC A 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnDroppedWeapon("Shot_gun",4);
		Stop;
	}
}

class DroppedPistol : DroppedWeapon
{
	States
	{
	Spawn:
		PIST G 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnDroppedWeapon("BrutalPistol",7);
		Stop;
	}
}

class DroppedSMG : DroppedWeapon
{	
	States
	{
	Spawn:
		SMGZ A 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnDroppedWeapon("BrutalSMG",7);
		Stop;
	}
}

class DroppedFragGrenade : DroppedWeapon
{
	Default
	{
		Scale 0.5;
		Speed 9;
		BounceSound "HandNadeBounce";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Spawn1","spawn2");
	Spawn1:
		GRND D 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Spawn2:
		GRND D 1 {A_SetRoll(roll - 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("AmmoFragGrenade");
		Stop;
	}
}

class DroppedPipeBomb : DroppedWeapon
{
	Default
	{
		Scale 0.4;
		Speed 9;
		BounceSound "PIPBOING";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,"Spawn1","Spawn2");
	Spawn1:
		PIPB B 1 {A_SetRoll(roll + 11.25);bThruActors = 0;}
		Loop;
	Spawn2:
		PIPB B 1 {A_SetRoll(roll - 11.25);bThruActors = 0;}
		Loop;
	Death:
		TNT1 A 0 A_SpawnItemEx("AmmoPipeBomb");
		Stop;
	}
}